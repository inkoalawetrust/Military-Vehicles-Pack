//Common effects used by most or all vehicles.

//Customizable explosion actor.
Class MVP_BigExplosion : Actor
{
	Default
	{
		RenderStyle "Add";
		MaxTargetRange 256; //The maximum range of the explosion.
		MeleeRange 96; //The range around the explosion in which actors take it's full damage.
		Damage 256; //The damage of the explosion, this property can only be changed with the SetDamage() function.
		+NeverTarget;
		+NotAutoaimed;
		+Bright;
	}
	States
	{
		Spawn:
			MCR3 A 3 NoDelay
			{
				A_Explode (MaxTargetRange,Damage,XF_HURTSOURCE|XF_NOTMISSILE|XF_EXPLICITDAMAGETYPE,True,MeleeRange,damagetype:'Explosion');
				A_QuakeEx (2,2,2,35*5,0,640,flags:QF_SCALEDOWN|QF_3D);
				A_StartSound ("SFX/BigExplosion",CHAN_AUTO,attenuation:0.2);
			}
			MCR3 BC 3;
			MCR3 DE 5;
			MCR3 FG 8;
			Stop;
	}
}

Class MVP_Explosion : Actor
{
	Default
	{
		RenderStyle "Add";
		MaxTargetRange 256; //The maximum range of the explosion.
		MeleeRange 48; //The range around the explosion in which actors take it's full damage.
		Damage 128; //The damage of the explosion, this property can only be changed with the SetDamage() function.
		+NeverTarget;
		+NotAutoaimed;
		+Bright;
	}
	States
	{
		Spawn:
			ZTXP A 2 NoDelay
			{
				//A_AttachLight ('Explosion',DynamicLight.PulseLight,"FFFF8F",256,0,DynamicLight.LF_Attenuate,param:2.857142857142857);
				A_Explode (MaxTargetRange,Damage,XF_HURTSOURCE|XF_NOTMISSILE|XF_EXPLICITDAMAGETYPE,True,MeleeRange,damagetype:'Explosion');
				A_StartSound ("SFX/Explosion",CHAN_AUTO,attenuation:0.5);
			}
			ZTXP BCDE 2;
			ZTXP FGHIJKLMNOPQRST 3;
			//TNT1 A 0 A_RemoveLight ('Explosion');
			Stop;
	}
}

//The reaction time property is used as the lifespan in tics.
Class MVP_Flame : Actor
{
	Default
	{
		RenderStyle "Add";
		ReactionTime (35*30);
		Scale 0.75;
		DamageType "Fire";
		Damage 5; //Damage done every 16 tics by the fire.
		MaxTargetRange 48; //Range of the flame.
		MeleeRange 24; //Radius in which the flame does its' full damage with no falloff.
		+NeverTarget;
		+NotAutoaimed;
		+NoDamageThrust;
		+Bright;
	}
	
	Vector3 Offsets;
	
	Override Void Tick()
	{
		Super.Tick();
		
		If (IsFrozen()) Return;
		
		//Stick to you master, if any.
		If (Master)
			SetOrigin (Master.Vec3Offset(Offsets.X,Offsets.Y,Offsets.Z),True);
		
		If (!InStateSequence(CurState,ResolveState ("Death")))
		{
			If (!(ReactionTime == 0) && GetAge() >= ReactionTime)
				SetStateLabel ("Death");
			
			If (GetAge() % 16 == 0)
				A_Explode (Damage*FRandom (0.75,1.5),MaxTargetRange,XF_HURTSOURCE|XF_NOTMISSILE,False,MeleeRange);
		}
	}
	
	States
	{
		Spawn:
			F1RE A 0 NoDelay
			{
				bXFlip = Random (0,1);
				
				A_StartSound ("SFX/Fire",CHAN_BODY,flags:CHANF_LOOPING,0.5);
				A_AttachLight ('Fire',DynamicLight.FlickerLight,"EBDB57",48,40,param:0.25);
			}
			#### ABCDEFGH 4;
			Goto Spawn+1;
		Death:
			#### A 4;
			#### B 4 A_SoundVolume (CHAN_BODY,0.8);
			#### # 0 {Scale -= (.15,.15);}
			#### C 4 A_SoundVolume (CHAN_BODY,0.6);
			#### # 0 {Scale -= (.15,.15);}
			#### D 4 A_SoundVolume (CHAN_BODY,0.4);
			#### # 0 {Scale -= (.15,.15);}
			#### E 4 A_SoundVolume (CHAN_BODY,0.2);
			#### # 0 {Scale -= (.15,.15);}
			TNT1 A 0 A_RemoveLight ('Fire');
			Stop;
		Sprites:
			F1RE ABCDEFGH 0;
			F2RE ABCDEFGH 0;
			Stop;
	}
}

Class MVP_Spark : Actor
{
	Default
	{
		RenderStyle "Add";
		Scale 0.05;
		+NoInteraction;
		+NoGravity;
	}
	States
	{
		Spawn:
			SPKY A 0 NoDelay
			{
				A_StartSound ("World/Spark", CHAN_AUTO, CHANF_DEFAULT, 1, ATTN_STATIC);
				Scale *= FRandom (0.8,2.0) ;
			}
			SPKY ABCDEF 4 Bright;
			TNT1 A 35;
			Stop;
	}
}
