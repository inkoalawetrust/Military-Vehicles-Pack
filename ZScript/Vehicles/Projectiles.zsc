//Generic projectiles used by multiple vehicles.

Class MVP_50CalBullet : MVP_RifleBullet{} //Old name.
Class MVP_LargeRifleBullet : MVP_BaseProjectile
{
	Default
	{
		Radius 4;
		Height 8;
		Speed 230;
		FastSpeed 250;
		XScale 0.2;
		YScale 0.166667; //0.2 / 1.2
		DamageFunction (Random (25,40));
		ProjectileKickback 32;
		RipperLevel 3;
		Obituary "%o was gunned down by a machine gun";
		Decal "50CalChip";
		DamageType "LargeRifleBullet";
		BounceSound "SFX/BulletRicochet";
		KAI_BaseProjectile.TrailSprite "MVP_Tracer";
		KAI_BaseProjectile.InitialTrailsSkipped 4;
		KAI_BaseProjectile.MaxTrails 128;
		KAI_BaseProjectile.RipDepth 5;
		+Ripper;
		+KAI_BaseProjectile.RipOnce;
		+KAI_BaseProjectile.Deflectable;
	}
	Bool HitLiquid;
	
	Override Int SpecialMissileHit (Actor Victim)
	{
		If (Super.SpecialMissileHit(Victim) == 1) Return 1;
		//If (Victim.bSolid && !Victim.bShootable) Return 1; //Don't stop at decorations that normally block projectiles.
		
		bExtremeDeath = (Victim.SpawnHealth() <= 200); //Obliterate any weak enemy.
		
		//Stop penetrating if the target has a lot of health.
		bRipper = (Victim.SpawnHealth() >= 500);
		Return -1;
	}
	
	Void Do50BMGSplash (Int LiquidType, Bool UpsideDown = False)
	{
		If (LiquidType == LIQUID_NONE) Return;
		HitLiquid = True;
		Actor Splash = SpawnLiquidSplash (LiquidType,volume:0.4,1.0);
		
		Double Size = FRandom (0.3,0.7);
		Splash.Scale.X = Splash.Scale.Y = (UpsideDown ? -Size : Size);
		Splash.bXFlip = Random (False,True);
	}
	
	States
	{
		Spawn:
			BUL1 A 0;
			BUL1 # 1 Light ("50CalTracerLight")
			{
				PlayFlybySound ("SFX/BulletFlyby",flags:CHANF_NOSTOP,0.75,ATTN_STATIC);
				Frame = UpdateProjectileElevation (2,2,0,1,1); //C, C, A, B, B
			}
			Loop;
		Death: //Bullet hit level geometry.
			TNT1 A 1
			{
				A_StopSound (CHAN_BODY);
				//The floor was hit.
				If (Pos.Z <= FloorZ)
					Do50BMGSplash(CheckLiquid(FloorPic));
				//The ceiling was hit.
				Else If (Pos.Z + Height >= CeilingZ)
					Do50BMGSplash(CheckLiquid(CeilingPic),True);
				
				If (HitLiquid) Destroy(); //Splash was spawned, so just delete yourself.
				Else A_StartSound ("Vehicle/APC/MGImpact");
			}
			Stop;
		XDeath: //Bullet hit a bleeding actor.
			TNT1 A 1; //Don't play the impact sound on bleeding actors.
			Stop;
		Crash: //Bullet hit a non bleeding actor.
			TNT1 A 1 A_StartSound ("Vehicle/APC/MGImpact");
			Stop;
	}
}

Class MVP_BulletCasing : Actor
{
	Default
	{
		Radius 4;
		Height 8;
		XScale 0.2;
		YScale 0.166667; //0.2 / 1.2
		+NoBlockmap;
		//+NoInteraction;
	}
	States
	{
		Spawn:
			BUL1 DEFG Random (2,4) A_CheckFloor ("Death");
			Loop;
		//Stay for 4 seconds, then fade over the course of 4 seconds.
		Death:
			BUL1 H 0 A_StartSound ("SFX/CasingImpact",volume:0.75,2.5);
			BUL1 H 140;
			BUL1 H 4 A_FadeOut (0.0142);
			Wait;
	}
}

//A smaller bullet projectile sort of based on the 7.62mm NATO round.
//TODO: Use the fucking belmondo bullet trace for these like I did the marines jesus.
Class MVP_RifleBullet : MVP_BaseProjectile
{
	Default
	{
		Radius 4;
		Height 6;
		Speed 200; //IRL, the M240 has a slightly slower muzzle velocity than the M2 Browning, so the rifle round is slightlky slower too, but not as slow as the autocannon.
		//I mean, autocannon shells are also fired at a higher muzzle velocity than bullets, yet the autocannon projectile is SLOWER instead of faster, but whatever.
		FastSpeed 220;
		DamageFunction (Random (10,20)); //The marines' rifles do 6 to 10 damage, so this is about double that. Why ? I don't fucking know, let's just say the marines use 5.56mm rounds or something.
		XScale 0.1;
		YScale 0.083333; //0.1 / 1.2
		ProjectileKickback 16;
		Decal "BulletChip";
		DamageType "RifleBullet";
		Obituary "%o found out that rifle rounds are more dangerous than in the movies";
		BounceSound "SFX/BulletRicochet";
		KAI_BaseProjectile.TrailSprite "MVP_Tracer";
		KAI_BaseProjectile.InitialTrailsSkipped 2;
		KAI_BaseProjectile.MaxTrails 64;
		KAI_BaseProjectile.TrailLineSpacing 1;
		+KAI_BaseProjectile.Deflectable;
	}
	
	Bool HitLiquid;
	
	Void DoRifleSplash (Int LiquidType, Bool UpsideDown = False)
	{
		If (LiquidType == LIQUID_NONE) Return;
		HitLiquid = True;
		Actor Splash = SpawnLiquidSplash (LiquidType,"SFX/WaterHit",1.0,1.0);
		
		Double Size = FRandom (0.2,0.3);
		Splash.Scale.X = Splash.Scale.Y = (UpsideDown ? -Size : Size);
		Splash.bXFlip = Random (False,True);
	}
	
	Override Void TrailSpawn (VisualThinker Trail)
	{
		Super.TrailSpawn (Trail);
		
		Trail.Scale.X = Trail.Scale.Y = 0.25;
	}
	
	//Mom, can we have MVP_50CalBullet ?
	//No, we have MVP_50CalBullet at home.
	//The MVP_50CalBullet at home:
	States
	{
		Spawn:
			BUL1 A 0;
			BUL1 # 1 Light ("RifleTracerLight")
			{
				PlayFlybySound ("SFX/BulletFlyby",flags:CHANF_NOSTOP,0.6,ATTN_STATIC,distance:384);
				Frame = UpdateProjectileElevation (2,2,0,1,1); //C, C, A, B, B
			}
			Loop;
		Death: //Bullet hit level geometry.
			TNT1 A 1
			{
				//The floor was hit.
				If (Pos.Z <= FloorZ)
					DoRifleSplash(CheckLiquid(FloorPic));
				//The ceiling was hit.
				Else If (Pos.Z + Height >= CeilingZ)
					DoRifleSplash(CheckLiquid(CeilingPic),True);
				
				If (HitLiquid) Destroy(); //Splash was spawned, so just delete yourself.
				Else A_StartSound ("Vehicle/APC/SmallMGImpact");
			}
			Stop;
		XDeath: //Bullet hit a bleeding actor.
			TNT1 A 1; //Don't play the impact sound on bleeding actors.
			Stop;
		Crash: //Bullet hit a non bleeding actor.
			TNT1 A 1 A_StartSound ("Vehicle/APC/SmallMGImpact");
			Stop;
	}
}

//5.56mm bullet
Class MVP_SmallRifleBullet : MVP_RifleBullet
{
	Default
	{
		Speed 170;
		FastSpeed 200;
		DamageFunction (Random (8,15));
		XScale 0.2;
		YScale 0.166666; //0.2 / 1.2
		ProjectileKickback 10;
		Decal "BulletChip";
		DamageType "SmallRifleBullet";
		+KAI_BaseProjectile.Deflectable;
	}
}

//Explosive gyroject
Class MVP_Gyrojet : MVP_BaseProjectile
{
	Default
	{
		Radius 4;
		Height 6;
		Speed 50;
		FastSpeed 70;
		DamageFunction (Random (12,20));
		XScale 0.2;
		YScale 0.16666; //0.2 / 1.2
		ProjectileKickback 32;
		ExplosionRadius 32;
		ExplosionDamage 32;
		Decal "Scorch";
		DamageType "Explosive";
		MissileType "MVP_RocketFlame";
		Obituary "%o was hit by a gyrojet";
		SeeSound "Vehicle/Drone/FireGyro";
		+SeekerMissile;
		+KAI_BaseProjectile.NoTrailLine;
		KAI_BaseProjectile.TrailSprite "MVP_RocketFireSprite";
	}
	
	Override Void PostBeginPlay()
	{
		Super.PostBeginPlay();
		
		Vel3DFromAngle (8,Angle,Pitch); //Start off slow and reach full speed over time. You know, gyrojet.
		DefSpeed = GetDefaultSpeed(GetClass());
		WeaveIndexXY = Random (0,63);
		WeaveIndexZ = Random (0,63);
	}
	
	//Rocket trail.
	Override Void TrailSpawn (VisualThinker Trail)
	{
		Super.TrailSpawn(Trail);
		If (Vel.Length() >= DefSpeed-MinVel)
		{
			Trail.Destroy();
			Return;
		}
		Trail.PostBeginPlay(); //Scale the trail NOW.
		KAI_Tracer(Trail).RanPostBeginPlayEarly = True;
		Trail.Pos = KAI_Math.Vec3OffsetRelative (Self,(-4,0,0));
		Trail.Flags |= SPF_ROLL;
		Trail.Roll = FRandom(-180,180);
		Trail.Scale *= FRandom(0.15,0.25);
		Trail.PostBeginPlay(); //Scale the trail NOW.
		KAI_Tracer(Trail).RanPostBeginPlayEarly = True;
		Trail.Vel.X = FRandom (-2,2);
		Trail.Vel.Y = FRandom (-2,2);
		Trail.Vel.Z = FRandom (-0.5,0.5);
	}
	
	Override Void Tick()
	{
		Super.Tick();
		
		If (IsFrozen() || !bMissile)
			Return;
		
		A_Weave(1,1,Weave,Weave);
		
		If (Vel.Length() >= DefSpeed-MinVel)
			Return;
		
		Double Calc = KAI_Math.LinearMap(GetAge(),0,35*2,8,DefSpeed);
		Calc = Clamp (Calc,8,DefSpeed);
		Vel3DFromAngle (Calc,Angle,Pitch); //Speed up with age.
		Weave = KAI_Math.LinearMap (Vel.Length(),8,DefSpeed,0,3); //Weave around more with age.
	}
	
	Void DoGyrojetSplash (Int LiquidType, Bool UpsideDown = False)
	{
		If (LiquidType == LIQUID_NONE) Return;
		HitLiquid = True;
		Actor Splash = SpawnLiquidSplash (LiquidType,"SFX/LiquidExplosion",0.5,1.5,ExplosionDamage*0.5,ExplosionRadius*0.5,ExplosionRadius*0.5,"Explosive",True);
		
		Double Size = FRandom (0.5,0.75);
		Splash.Scale.X = Splash.Scale.Y = (UpsideDown ? -Size : Size);
		Splash.bXFlip = Random (False,True);
	}
	
	Double DefSpeed;
	Double Weave;
	Bool HitLiquid;
	
	States
	{
		Spawn:
			BUL1 I 0;
			BUL1 # 4 Light ("RifleTracerLight")
			{
				Frame = UpdateProjectileElevation (10,10,8,9,9); //K, K, I, J, J
				KAI_SeekerMissile (1,0.75,SMF_CURSPEED|SMF_SMART,(32,32,32));
				If (Vel.Length() >= DefSpeed-MinVel)
				{
					bSeekerMissile = False;
					//bNoTrailLine = False; //Stop the rocket fire.
					Return FindState ("Cruise");
				}
				Return State(Null);
			}
			Loop;
		Cruise: //Finished acceleration
			BUL1 # 4
			{
				PlayFlybySound ("SFX/BulletFlyby",flags:CHANF_NOSTOP,0.6,ATTN_STATIC,distance:384);
				Frame = UpdateProjectileElevation (10,10,8,9,9); //K, K, I, J, J
			}
			Loop;
		Death:
			TNT1 A 0
			{
				//The floor was hit.
				If (Pos.Z <= FloorZ)
					DoGyrojetSplash(CheckLiquid(FloorPic));
				//The ceiling was hit.
				Else If (Pos.Z + Height >= CeilingZ)
					DoGyrojetSplash(CheckLiquid(CeilingPic),True);
				
				If (HitLiquid) Destroy(); //Splash was spawned, so just delete yourself.
			}
			TNT1 A 0
			{
				Roll = Random (0,360);
				A_SetScale (0.5);
				A_SetRenderStyle (1.0,STYLE_ADD);
				MakeAlliesImmuneToExplosion (Target,ExplosionRadius);
				A_Explode (ExplosionDamage,ExplosionRadius,XF_CIRCULAR|XF_CIRCULARTHRUST,False,ExplosionRadius);
				RemoveAllyExplosionImmunity();
				A_StartSound ("SFX/Explosion",volume:0.5,attenuation:2);
			}
			GAXP ABCDEFGHIJ 2 Bright;
			Stop;
	}
}

//Ugh, I need to animate this with ANIMDEFS and also have custom age timings for transitioning from fire to smoke.
Class MVP_RocketFireSprite : MVP_Tracer
{
	Override Void SetProperties()
	{
		Super.SetProperties();
		DefaultSprite = TexMan.CheckForTexture("RSMKA0",TexMan.Type_Sprite);
		NoWarp = True;
		DestroyIfNoOwner = False;
		Flags |= SPF_FULLBRIGHT;
		SetTranslation('BrightZDoomFire');
	}
	
	Int Tics;
	
	Override Void Tick()
	{
		Super.Tick();
		
		If (IsFrozen()) Return;
		
		Tics++;
		
		If (Tics == 1) //Normal fire brightness.
			SetTranslation ('ZDoomFire');
		Else If (Tics == 3) //Switch to smoke mode.
		{
			SetTranslation ('None');
			//SetRenderStyle (STYLE_TRANSLUCENT);
			//Alpha = 0.8;
			Flags &= ~SPF_FULLBRIGHT;
		}
		Else If (Tics >= 6) //Done
			Destroy();
	}
}

Class MVP_AutocannonShell : MVP_BaseProjectile
{
	Default
	{
		Radius 12;
		Height 16;
		Speed 180;
		FastSpeed 200;
		XScale 0.5;
		YScale 0.416666; //0.5 / 1.2
		DamageFunction (Random (60,80));
		ProjectileKickback 128;
		Obituary "%o took a 30mm round to the face";
		Decal "30mmScorch";
		DamageType "Autocannon";
		KAI_BaseProjectile.TrailSprite "MVP_AutocannonTrail";
		KAI_BaseProjectile.TrailDelay 1;
		KAI_BaseProjectile.InitialTrailsSkipped 16;
		KAI_BaseProjectile.TrailFadeAndScaleStart 64;
		KAI_BaseProjectile.TrailLineSpacing 4;
		KAI_BaseProjectile.MaxTrails 128;
		KAI_BaseProjectile.ScaleAmountX -0.05;
		KAI_BaseProjectile.ScaleAmountY -0.05;
		KAI_BaseProjectile.FadeAmount -0.05;
	}
	
	Override Void Tick()
	{
		Super.Tick();
		If (IsFrozen()) Return;
		If (bMissile) PlayFlybySound ("SFX/BulletFlyby",CHAN_VOICE,CHANF_NOSTOP,0.90,2.5,distance:400);
		
		//If you are an airburst shell and have a specific target.
		If (IsAirburst && Tracer && bMissile) //bMissile is removed when projectiles explode. Making this check not run on exploded shells.
		{
			//And are within blast range of the tracer.
			If (!IsDead(Tracer) && Distance3DSquared (Tracer) <= 176*176)
			{
				FLineTraceData TracerCheck;
				
				LineTrace (Angle,192,Pitch,TRF_THRUHITSCAN|TRF_SOLIDACTORS|TRF_BLOCKSELF,data:TracerCheck);
				
				//If you aren't going to directly impact the tracer.
				If (TracerCheck.HitActor != Tracer)
					SmokeCounter++; //Give it another chance in the next tick.
				Else
					SmokeCounter = 0;
				
				//If you still haven't lined up to hit the tracer. Then airburst.
				If (SmokeCounter > 1)
				{
					AirBursted = True;
					ExplodeMissile();
				}
			}
		}
	}
	
	Override Int SpecialMissileHit (Actor Victim)
	{
		If (Super.SpecialMissileHit(Victim) == 1) Return 1;
		
		If (Victim.SpawnHealth() <= 400) bExtremeDeath = True; //Always gib weak enough enemies.
		Else bExtremeDeath = False;
		
		Return -1;
	}
	
	Void DoAutocannonSplash (Int LiquidType, Bool UpsideDown = False)
	{
		If (LiquidType == LIQUID_NONE) Return;
		HitLiquid = True;
		Actor Splash = SpawnLiquidSplash (LiquidType,explosiondamage:96,48,0,'Explosive', True);
		
		Double Size = FRandom (1.8,2.2);
		Splash.Scale.X = Splash.Scale.Y = (UpsideDown ? -Size : Size);
		Splash.bXFlip = Random (False,True);
	}
	
	Bool HitLiquid; //Removes the autocannon shell if it hit a liquid texture.
	Bool IsAirburst; //Should the shell prematurely explode if it's within 150 map units of its' tracer ?
	Bool Airbursted; //Did the shell explode in an airburst ? Ensures that airbursted shells ALWAYS go to the AirBlast state.
	DynamicLight Light;
	
	States
	{
		Spawn:
			30MS A 0;
			30MS # 4 Light ("AutocannonTracer")
			{
				A_FaceMovementDirection ();
				Frame = UpdateProjectileElevation (2,2,0,1,1); //C, C, A, B, B
			}
			Wait;
		Death: //Shell hit level geometry.
			TNT1 A 0
			{
				A_StopSound (CHAN_VOICE);
				If (Airbursted) //The shell detonated mid air to hit a target that it missed.
				{
					SpawnHitscanShrapnel(128);
					A_SetScale (1.5,1.5);
					A_SetRenderStyle (1.0,Style_Add);
					Return ResolveState ("AirBlast");
				}
				
				//The floor was hit.
				If (Pos.Z <= FloorZ)
					DoAutocannonSplash(CheckLiquid(FloorPic));
				//The ceiling was hit.
				Else If (Pos.Z + Height >= CeilingZ)
					DoAutocannonSplash(CheckLiquid(CeilingPic),True);
				
				If (HitLiquid) Destroy(); //Splash was spawned, so just delete yourself.
				
				Return State (Null);
			}
			TNT1 A 0
			{
				A_SetScale (1.5,1.5);
				A_SetRenderStyle (1.0,Style_Add);
				If (Pos.Z + Height >= CeilingZ) Scale.Y = -1.5; //If a ceiling was hit, invert the explosion.
				Else If (Pos.Z >= FloorZ+24) Return ResolveState ("AirBlast"); //If the explosion happened above 24 map units from the ground, trigger the air explosion.
				//A_AttachLight ("ExplosionLight",DynamicLight.PulseLight,"FFE2B3",512,0,DYNAMICLIGHT.LF_ATTENUATE,(0,0,1),param:1.714285714285714*3.4); //Only for ground explosions !
				//SetOrigin ((Pos.X,Pos.Z,FloorZ),False); //Otherwise stick to the ground to explode if you didn't hit level geometry too far above the ground.
				Return State (Null);
			}
			GXPL A 2 Bright {If (Light) Light.Args[3]-= 8.53;}
			GXPL B 4 Bright
			{
				Light = DynamicLight(Spawn ("PointLight",Vec3Offset (0,0,8)));
				If (Light)
				{
					Light.Args[3] = 128; //Intensity
					Light.Args[0] = 255; Light.Args[1] = 225; Light.Args[2] = 179; //RGB
				}
				A_StartSound ("Vehicle/APC/AutocannonHit",attenuation:0.3);
				MakeAlliesImmuneToExplosion (Target, 128);
				A_Explode (Int(96*RadiusDamageFactor),128,XF_EXPLICITDAMAGETYPE,True,32,damagetype:'Explosive');
				RemoveAllyExplosionImmunity();
			}
			GXPL CDEFGHIJKLMNO 4 Bright {If (Light) Light.Args[3]-= 9.84;}
			TNT1 A 0 {If (Light) Light.Destroy();}
			Stop;
		XDeath: //Shell hit a bleeding actor.
			Goto AirBlast;
		Crash: //Shell hit a non bleeding actor.
			Goto AirBlast;
		AirBlast:
			TNT1 A 0 //Stop moving in the air, and add some random rotation for variety.
			{
				Light = DynamicLight(Spawn ("PointLight",Vec3Offset (0,0,8)));
				If (Light)
				{
					Light.Args[3] = 128; //Intensity
					Light.Args[0] = 255; Light.Args[1] = 253; Light.Args[2] = 117; //RGB
				}
				A_StartSound ("Vehicle/APC/AutocannonHit",attenuation:0.15); //Air explosions makes sound that travels further.
				A_Stop();
				A_SetRenderStyle (1.0,Style_Add);
				A_SetScale (1.5,1.5);
				Gravity = 0;
				bRollSprite = True;
				Roll = FRandom (0,180);
			}
			GAXP A 4 Bright {If (Light) Light.Args[3]-= 32;}
			GAXP B 4 Bright
			{
				If (Light) Light.Args[3]-= 32;
				MakeAlliesImmuneToExplosion (Target, 128);
				A_Explode (Int(96*RadiusDamageFactor),192,XF_EXPLICITDAMAGETYPE,True,48,damagetype:'Explosive');
				RemoveAllyExplosionImmunity();
			}
			GAXP C 4 {If (Light) Light.Args[3]-= 32;}
			GAXP C 0 {If (Light) Light.Destroy();}
			GAXP DEFGHIJ 4 Bright;
			Stop;
	}
}

Class MVP_AutocannonTrail : MVP_Tracer
{
	Override Void SetProperties()
	{
		Super.SetProperties();
		DefaultScale = (1.1,1.1);
	}
}

Class MVP_AutocannonCasing : Actor
{
	Default
	{
		Radius 12;
		Height 16;
		Speed 180;
		FastSpeed 200;
		XScale 0.25;
		YScale 0.208333; //0.25 / 1.2
		+NoBlockmap;
		//+NoInteraction;
	}
	States
	{
		Spawn:
			30MS DEFG Random (2,4) A_CheckFloor ("Death");
			Loop;
		//Stay for 5 seconds, then fade over the course of 4 seconds.
		Death:
			30MS H 0 A_StartSound ("SFX/CasingImpact",attenuation:2.5);
			30MS H 175;
			30MS H 4 A_FadeOut (0.0142);
			Wait;
	}
}