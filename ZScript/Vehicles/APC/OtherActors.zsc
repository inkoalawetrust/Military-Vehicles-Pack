//$GZDB_SKIP
Class MVP_APCMachineGun : MVP_BaseTurret
{
	Default
	{
		Health 400;
		GibHealth 250;
		Height 32;
		DeathHeight 32;
		Radius 32;
		Mass 1200;
		FriendlySeeBlocks 80; //A bit over 10000 map unit sight range, useful for vehicles with actual guns to attack with.
		MVP_BaseTurret.MaximumPitch -60;
		MVP_BaseTurret.MinimumPitch 20;
		MVP_BaseTurret.TurnSoundRadius 4;
		MVP_BaseTurret.CombatAngleTurnRate 55;
		MVP_BaseTurret.CombatPitchTurnRate 30;
		Tag "Machine Gun Turret";
		Obituary "%o was gunned down by a machine gun turret";
		DamageFactor "Autocannon", 0.8;
		AttackSound "Vehicle/APC/MGFire";
		MVP_BaseTurret.TurnSound "Vehicle/APC/TurretTurn";
		+MissileMore;
		//+MVP_BaseVehicle.ScareMarines;
	}
	
	Const DualMGAttackHeight = 12;
	
	Array <Actor> HitActors;
	MVP_APCMachineGunLOFCheck MarineWarner; //A permanent pointer to a dual MG LOF check tracer. So that it can be fired when the turret is firing to warn marines.
	
	Bool MGLOFCheck()
	{
		Let Check = New ("MVP_APCMachineGunLOFCheck");
		
		Vector3 Direction = (AngleToVector(Angle, Cos(Pitch)), -Sin(Pitch));
		
		Double Distance = Target ? Distance3D (Target)+32 : MaxTargetRange;
		Check.Shooter = Self;
		Check.Other = Target;
		Check.Trace ((Pos.X,Pos.Y,Pos.Z+DualMGAttackHeight),CurSector,Direction,Distance,0);
		
		If (Check.BadLOF)
		{
			bDontStayAround = True;
			Check.Destroy();
			Return False;
		}
		bDontStayAround = False;
		Check.Destroy();
		Return True;
	}
	
	Override Bool ShouldAttack (Bool NoStateJump)
	{
		If (!MVP_BaseTurret.ShouldAttack()) Return False;
		If (!CheckFOV (Target,90/2)) Return False;
		If (IsOverPitchLimits (Target,DualMGAttackHeight,Target.Height/2) != ELEVATION_INBOUNDS) Return False;
		
		If (Target != Self && MVP_CheckMissileRange(Target) && !IsDead (Target) && MVP_IsHostile (Self,Target)&& MGLOFCheck())
		{
			If (!NoStateJump) SetStateLabel ("Fire");
			Return True;
		}
		
		Return False;
	}
	
	Override Void PostBeginPlay ()
	{
		Super.PostBeginPlay();
		
		MissileState = FindState ("Fire");
		AttackStates.Push (FindState("Fire",True));
		MarineWarner = New ("MVP_APCMachineGunLOFCheck"); //Create the warning trace for marines.
	}
	
	Override Void Die (Actor Source, Actor Inflictor, Int DmgFlags, Name MeansOfDeath)
	{
		Super.Die (Source, Inflictor, DmgFlags, MeansOfDeath);
		If (MarineWarner) MarineWarner.Destroy();
		If (Master) Master.SetTag (Master.Default.GetTag());
	}
	
	Override Void OnDestroy()
	{
		If (Master) Master.SetTag (Master.Default.GetTag());
		If (MarineWarner) MarineWarner.Destroy();
		Super.OnDestroy();
	}
	
	Override Void Tick()
	{
		Super.Tick();
		
		If (IsFrozen()) Return;
		//When shooting, fire a ray that alerts any marines that would be caught in the line of fire.
		If (IsInAttackState())
		{
			//MVP_FindInLOF (0,INT.MAX,0,(0,0,12),foundactors:HitActors);
			Vector3 Direction = (AngleToVector(Angle, Cos(Pitch)), -Sin(Pitch));
			
			Double Distance = Target ? Distance3D (Target)+32 : MaxTargetRange;
			MarineWarner.Shooter = Self;
			MarineWarner.Other = Target;
			MarineWarner.OnlyFindActors = True;
			MarineWarner.Trace ((Pos.X,Pos.Y,Pos.Z+DualMGAttackHeight),CurSector,Direction,Distance,0);
			HitActors.Move(MarineWarner.Mobjs);
			//Check all the actors the trace hit.
			Name TheMarine = "SmartMarine";
			ForEach (Mobj : HitActors)
				If (Mobj && !IsDead (Mobj) && Mobj Is TheMarine && IsHostile(Mobj))
					WarnSpecificMarine(Mobj); //Then warn them.
			HitActors.Clear();
		}
	}
	
	States
	{
		Spawn:
			APC3 A 0;
			APC3 # 1
			{
				RotateToVehicle();
				A_LookEx (maxseedist:MaxTargetRange,DEFAULT_HEARDISTANCE);
				Frame = UpdateTurretElevation (3,0,6,9); //D, A, G, J
			}
			Loop;
		See:
			APC3 # 1
			{
				A_FaceTarget (5,5,flags:FAF_MIDDLE);
				RotateToVehicle();
				A_Chase (Null,Null,CHF_DONTIDLE|CHF_DONTMOVE|CHF_NODIRECTIONTURN);
				MVP_Chase (MVPC_NOCHASE);
				UpdateTurretSnapTimer();
				Frame = UpdateTurretElevation (3,0,6,9); //D, A, G, J
			}
			Loop;
		Fire:
			APC3 ###### 1
			{
				//A_FaceTarget(0,0,flags:FAF_MIDDLE);
				Frame = UpdateTurretElevation (3,0,6,9); //D, A, G, J
			}
			APC3 # 3 Light ("50CalFiringLight") Bright //Right shot
			{
				//A_FaceTarget(0,0,flags:FAF_MIDDLE);
				A_StartSound (AttackSound,CHAN_WEAPON,CHANF_OVERLAP,1.0,0.3);
				Actor Proj = A_SpawnProjectile ("MVP_50CalBullet",12,-8,flags:CMF_AIMDIRECTION,Pitch); //pew pew
				HandleTargetPrediction (Proj,(0.8,-0.8),(1.0,-1.0),1280);
				Frame = UpdateTurretElevation (4,1,7,10); //E, B, H, K
			}
			APC3 # 3 Light ("50CalFiringLight") Bright //Left shot
			{
				//A_FaceTarget(0,0,flags:FAF_MIDDLE);
				A_StartSound (AttackSound,CHAN_WEAPON,CHANF_OVERLAP,1.0,0.3);
				Actor Proj = A_SpawnProjectile ("MVP_50CalBullet",12,8,flags:CMF_AIMDIRECTION,Pitch); //pew pew
				HandleTargetPrediction (Proj,(0.8,-0.8),(1.0,-1.0),1280);
				Frame = UpdateTurretElevation (5,2,8,11); //F, C, I, L
			}
			APC3 # 1
			{
				//A_FaceTarget(0,0,flags:FAF_MIDDLE);
				//If you have no target, or it's dead.
				If (!Target || IsDead (Target))
				{
					A_ClearTarget();
					LookForPlayers (True); //Look for a new target.
				}
				
				If (Random2[pr_monsterrefire]() < 48 &&
				((Target && IsOverPitchLimits (Target,12,Target.Height/2) != ELEVATION_INBOUNDS) ||
				!MGLOFCheck()))
				{
					A_ClearTarget();
					Return ResolveState ("See");
				}
				
				HandleChaseTimer();
				
				Frame = UpdateTurretElevation (3,0,6,9); //D, A, G, J
				Return State (Null);
			}
			APC3 # 0 MVP_TurretRefire (48,"See",True);
			Goto Fire+6;
		Death:
			APC3 M 0 {Actor Boom = Spawn ("MVP_Explosion",Pos); Boom.bNoGravity = True;}
			APC3 M 1 RotateToVehicle(RTVF_NOTARGETCHECK);
			Wait;
		XDeath:
			APC3 N 0
			{
				Height = 16;
				Actor Boom = Spawn ("MVP_Explosion",Pos);
				Boom.Scale *= 1.75;
				Boom.SetDamage (192);
				Boom.MeleeRange = 64;
				Boom.bNoGravity = True;
				
				Actor Flame = Spawn ("MVP_Flame",Pos);
				MVP_Flame(Flame).Offsets = (0.1,0,4);
				Flame.Scale.X = Flame.Scale.Y = 1.0;
				Flame.Master = Self;
				Flame.ReactionTime *= Int(FRandom (1.25,4.25));
				Flame.SetDamage (8);
				Flame.MaxTargetRange = 64;
				Flame.MeleeRange = 32;
				
				A_SpawnItemEx ("MVP_DestroyedMGTurretGun",16,0,12,FRandom (16,32),FRandom (-6,6),FRandom (2,6));
			}
			APC3 N 1 RotateToVehicle(RTVF_NOTARGETCHECK);
			Wait;
		GenericFreezeDeath:
			APC3 # 0 {Frame = UpdateTurretElevation (3,0,6,9); /*D, A, G, J*/}
			Goto Super::GenericFreezeDeath;
	}
}

Class MVP_APCMachineGunLOFCheck : MVP_LOFRaycast
{
	Const FiftyCalRipLevel = 3; //This has to be changed if MVP_50CalBullets' ripper level is changed.
	Const MaxRippedActors = 5; //Same as above, but this is the general count of how many actors a bulelt can go through before stopping.
	Bool OnlyFindActors; //Used to make the dual MG warn marines it is firing at.
	Override ETraceStatus TraceCallback()
	{
		Actor Mobj = Results.HitActor;
		
		If (!Shooter) Return Trace_Stop; //Not sure if this should return BadLOF too.
		
		AddToMobjList (Mobj);
		
		If (Mobj == Shooter.Master) Return Trace_Skip;
		
		//The target is in the line of fire. Shoot them anyway.
		If (Mobj == Other && !OnlyFindActors)
			Return Trace_Stop;
		
		Bool IsEnemy = Shooter.IsHostile(Mobj); //Caching
		
		//Return BadLOF if the actor the trace found would stop the .50 cal round from ripping. Except if it's another enemy anyway.
		If ((IsInanimateObject(Mobj) || !IsEnemy) && CantRipActor (Mobj,FiftyCalRipLevel))
		{
			BadLOF = True;
			Return Trace_Stop;
		}
		
		//The machine gun turret can't shoot through solid enemy corpses that strong.
		If (IsEnemy && IsSolidCorpse (Mobj) && Mobj.SpawnHealth() >= 500 )
		{
			BadLOF = True;
			Return Trace_Stop;
		}
		
		//The trace specifically hit a projectile blocking line. e.g a window linedef or a polyobject.
		If (BlockingLineInTheWay (Results.HitLine,0))
		{
			BadLOF = True;
			Return Trace_Stop;
		}
		
		If (HitLevelGeometry (Results))
		{
			BadLOF = True;
			Return Trace_Stop;
		}
		
		Return Trace_Skip;
	}
}

//Somewhat based on the MG.
Class MVP_APCAutocannon : MVP_APCMachineGun
{
	Default
	{
		Health 450;
		GibHealth 300;
		Height 20;
		DeathHeight 20;
		Mass 1500;
		Tag "Autocannon Turret";
		Obituary "%o was blown up by an autocannon";
		AttackSound "Vehicle/APC/AutocannonFire";
		MVP_BaseVehicle.VehicleTurret "MVP_APCMGTurret";
		MVP_BaseTurret.MaximumPitch -50;
		MVP_BaseTurret.MinimumPitch 20;
		MVP_BaseTurret.TurnSoundRadius 4;
		+MissileEvenMore;
	}
	
	Bool FocusFire; //Turned on by the APC, then passed over to the small MG on the autocannon.
	Mixin MVP_AutocannonWarningZoneHandling;
	
	Override Void PostBeginPlay ()
	{
		Super.PostBeginPlay();
		
		MissileState = FindState ("Fire");
		AttackStates.Push (FindState("Fire",True));
	}
	
	Override Void SpawnVehicleTurret ()
	{
		Vector3 Original = TurretOffsets;
		TurretOffsets = (-0.1,0,1.1); //Temporarily set the autocannons' turret offsets to this, for spawning the small MG, then revert to the previous offsets.
		Super.SpawnVehicleTurret();
		If (FocusFire && Turret) MVP_APCMGTurret(Turret).FocusFire = True;
		If (bNoTargetPrediction) Turret.bNoTargetPrediction = True;
		TurretOffsets = Original;
	}
	
	Override Bool ShouldAttack (Bool NoStateJump)
	{
		If (!MVP_BaseTurret.ShouldAttack()) Return False;
		If (!CheckFOV (Target,90/2)) Return False;
		If (IsOverPitchLimits (Target,12,Target.Height/2) != ELEVATION_INBOUNDS) Return False;
		
		If (Target != Self && MVP_CheckMissileRange(Target) && !IsDead (Target) && MVP_IsHostile (Self,Target) && MVP_TurretCheckLOF (Target,400,128,(0,0,12)))
		{
			If (!NoStateJump)
			{
				SetStateLabel ("Fire");
				WarningZone = WarnMarines (0,128); //Create a warning zone to warn marines of wherever the autocannon is aiming.
				If (WarningZone) WarningZone.SetOrigin (GetAttackLocation(),True);
			}
			Return True;
		}
		
		Return False;
	}
	
	States
	{
		Spawn:
			APC2 A 0;
			APC2 # 1
			{
				RotateToVehicle();
				A_LookEx (maxseedist:MaxTargetRange,DEFAULT_HEARDISTANCE);
				Frame = UpdateTurretElevation (2,0,4,6); //C, A, E, G
			}
			Loop;
		See:
			APC2 # 1
			{
				If (WarningZone) WarningZone.Destroy();
				A_FaceTarget (5,5,flags:FAF_MIDDLE);
				RotateToVehicle();
				A_Chase (Null,Null,CHF_DONTIDLE|CHF_DONTMOVE|CHF_NODIRECTIONTURN);
				MVP_Chase (MVPC_NOCHASE);
				UpdateTurretSnapTimer();
				Frame = UpdateTurretElevation (2,0,4,6); //C, A, E, G
			}
			Loop;
		Fire:
			APC2 ###### 1
			{
				If (bNoCombatTraverse) A_FaceTarget (0,0,flags:FAF_MIDDLE);
				Frame = UpdateTurretElevation (2,0,4,6); //C, A, E, G
			}
			APC2 # 3 Light ("AutocannonFlash") Bright
			{
				If (bNoCombatTraverse) A_FaceTarget (0,0,flags:FAF_MIDDLE);
				A_StartSound (AttackSound,CHAN_WEAPON,CHANF_OVERLAP,1.0,0.25);
				Actor Proj = A_SpawnProjectile ("MVP_AutocannonShell",16,flags:CMF_AIMDIRECTION,Pitch); //pew pew
				HandleTargetPrediction (Proj,(0.3,-0.3),(0.45,-0.45),768);
				//If the enemy the shell was fired at is a flying enemy vulnerable to radius damage. Then use the proximity fuze.
				If (Target && IsFlying (Target) && !Target.bNoRadiusDmg)
				{
					Proj.Tracer = Target;
					MVP_AutocannonShell(Proj).IsAirburst = True;
				}
				Frame = UpdateTurretElevation (3,1,5,7); //D, B, F, H
			}
			APC2 ### 8
			{
				If (bNoCombatTraverse) A_FaceTarget(0,0,flags:FAF_MIDDLE);
				//If you have no target, or it's dead.
				If (!Target || IsDead (Target))
				{
					A_ClearTarget();
					LookForPlayers (True); //Look for a new target.
				}
				
				If (Random2[pr_monsterrefire]() < 20 &&
				((Target && IsOverPitchLimits (Target,12,Target.Height/2) != ELEVATION_INBOUNDS) ||
				!MVP_TurretCheckLOF (Target,400,128,(0,0,12))))
				{
					A_ClearTarget();
					Return ResolveState ("See");
				}
				
				HandleChaseTimer();
				
				Frame = UpdateTurretElevation (2,0,4,6); //C, A, E, G
				Return State (Null);
			}
			APC2 # 0 MVP_TurretRefire (10,"See",True);
			Goto Fire+6;
		Death: //When TG5 finishes the sprites for the gunner getting out the turret, add code to make them work here.
			APC2 Q 0
			{
				Actor Boom = Spawn ("MVP_Explosion",Pos);
				Boom.Scale *= 1.75;
				Boom.SetDamage (192);
				Boom.MeleeRange = 64;
				Boom.bNoGravity = True;
			}
			APC2 Q 1 RotateToVehicle (RTVF_NOTARGETCHECK);
			Wait;
		XDeath:
			APC2 U 0
			{
				Height = 16;
				Actor Boom = Spawn ("MVP_Explosion",Pos);
				Boom.Scale *= 2.5;
				Boom.SetDamage (256);
				Boom.MeleeRange = 96;
				Boom.MaxTargetRange = 384;
				Boom.bNoGravity = True;
				
				Actor Flame = Spawn ("MVP_Flame",Pos);
				MVP_Flame(Flame).Offsets = (0.1,0,4);
				Flame.Scale.X = Flame.Scale.Y = 1.0;
				Flame.Master = Self;
				Flame.ReactionTime *= Int(FRandom (1.25,4.25));
				Flame.SetDamage (8);
				Flame.MaxTargetRange = 64;
				Flame.MeleeRange = 32;
				
				A_SpawnItemEx ("MVP_APCHatch",-12,6,24,FRandom (6,-6),FRandom (6,-6),FRandom (18,24));
			}
			APC2 U 1 RotateToVehicle(RTVF_NOTARGETCHECK);
			Wait;
		GenericFreezeDeath:
			APC2 # 0 {Frame = UpdateTurretElevation (2,0,4,6); /*C, A, E, G*/}
			Goto MVP_BaseVehicle::GenericFreezeDeath;
	}
}

//Generic warning zone handling copied between the autocannon and its' small MG turret.
Mixin Class MVP_AutocannonWarningZoneHandling
{
	Actor WarningZone; //The warning zone the turret creates right before it fires.
	Override Void Tick()
	{
		MVP_BaseTurret.Tick(); //Super.Tick would be the dual MGs' Tick(). Which is bad because the MGs' Tick() handles the marine warnings for its' .50 BMG rounds.
		If (IsFrozen()) Return;
		If (WarningZone) //Move the warning to wherever the shell will go, warning marines in advance.
			WarningZone.SetOrigin (GetAttackLocation(),True);
	}
	
	Override Void OnDestroy()
	{
		Super.OnDestroy();
		If (WarningZone) WarningZone.Destroy();
	}
	
	Override Void Die (Actor Source, Actor Inflictor, Int DmgFlags, Name MeansOfDeath)
	{
		If (WarningZone) WarningZone.Destroy();
		Super.Die (Source, Inflictor, DmgFlags, MeansOfDeath);
	}
	
	//Fire a ray from the direction the turret is facing and return a position to where the ray stopped, which should also be where the projectile with hit.
	Override Vector3 GetAttackLocation ()
	{
		Let Check = New ("MVP_GenericWarnMarineCheck");
		
		Vector3 Direction = (AngleToVector(Angle, Cos(Pitch)), -Sin(Pitch));
		
		Check.Shooter = Self;
		Check.Other = Target;
		Check.Trace ( Level.Vec3Offset (Pos,(0,0,14)), CurSector, Direction, MaxTargetRange, 0 );
		
		Return Check.Results.HitPos;
	}
}

//The smaller MG turret that is attached on the autocannon.
Class MVP_APCMGTurret : MVP_BaseTurret
{
	Default
	{
		Health 125;
		GibHealth 60;
		Height 12;
		DeathHeight 12;
		Radius 24;
		Mass 80;
		RipLevelMin 1;
		MaxTargetRange 6144; //Reduced range compared to most attacks.
		FriendlySeeBlocks 48; //Reduced sight range too.
		MVP_BaseTurret.MaximumPitch -50; //Doesn't elevate quite as high as the other APC turrets.
		MVP_BaseTurret.MinimumPitch 20;
		MVP_BaseTurret.CombatAngleTurnRate 50;
		MVP_BaseTurret.CombatPitchTurnRate 37;
		Obituary "%o found out that rifle rounds are more dangerous than in the movies";
		Tag "7.62mm Turret";
		AttackSound "Vehicle/APC/SmallMGFire";
		DamageType "RifleBullet";
		DamageFactor "Autocannon", 0.95;
		DamageFactor "Melee", 0.4;
		DamageFactor "Electric", 0.4;
		DamageFactor "ExplosiveImpact", 0.825;
		DamageFactor "Explosion", 0.825;
		DamageFactor "HitScan", 0.9;
		+MissileMore;
		+MissileEvenMore;
	}
	
	Bool FocusFire; //Makes the MG not actively avoid sharing the same target as the turret, even if the shared target isn't a powerful enemy.
	Mixin MVP_AutocannonWarningZoneHandling;
	
	Override Void PostBeginPlay ()
	{
		Super.PostBeginPlay();
		
		MissileState = FindState ("Fire");
		AttackStates.Push (FindState("Fire",True));
	}
	
	Override Bool ShouldAttack (Bool NoStateJump)
	{
		If (!MVP_BaseTurret.ShouldAttack()) Return False;
		If (!CheckFOV (Target,90/2)) Return False;
		If (IsOverPitchLimits (Target,8,Target.Height/2) != ELEVATION_INBOUNDS) Return False;
		
		If (Target != Self && MVP_CheckMissileRange(Target) && !IsDead (Target) && MVP_IsHostile (Self,Target) && MVP_TurretCheckLOF (Target,150,0,(0,0,8)))
		{
			If (!NoStateJump)
			{
				SetStateLabel ("Fire");
				WarningZone = WarnMarines (0,48); //Create a warning zone to warn marines of wherever the machine gun is aiming.
				If (WarningZone) WarningZone.SetOrigin (GetAttackLocation(),True);
			}
			Return True;
		}
		
		Return False;
	}
	States
	{
		Spawn:
			APC2 I 0;
			APC2 # 1
			{
				RotateToVehicle();
				A_LookEx (maxseedist:MaxTargetRange,DEFAULT_HEARDISTANCE);
				Frame = UpdateTurretElevation (10,8,12,14); //K, I, M, O
			}
			Loop;
		See:
			APC2 # 1
			{
				If (WarningZone) WarningZone.Destroy();
				A_FaceTarget (6,6,flags:FAF_MIDDLE);
				RotateToVehicle();
				A_Chase (Null,Null,CHF_DONTIDLE|CHF_DONTMOVE|CHF_NODIRECTIONTURN);
				MVP_Chase (MVPC_NOCHASE);
				UpdateTurretSnapTimer();
				Frame = UpdateTurretElevation (10,8,12,14); //K, I, M, O
			}
			Loop;
		Fire:
			APC2 ###### 1
			{
				//A_FaceTarget (0,0,flags:FAF_MIDDLE);
				Frame = UpdateTurretElevation (10,8,12,14); //K, I, M, O
			}
			APC2 # 4 Light ("RifleFiringLight") Bright
			{
				//A_FaceTarget (0,0,flags:FAF_MIDDLE);
				
				//Play at half the volume if the autocannon is playing its' firing sound. To emphasize its' volume.
				If (Master && Master.IsActorPlayingSound (CHAN_WEAPON))
					A_StartSound (AttackSound,CHAN_WEAPON,CHANF_OVERLAP,0.75,0.4);
				Else
					A_StartSound (AttackSound,CHAN_WEAPON,CHANF_OVERLAP,1.0,0.4);
				
				Actor Proj = A_SpawnProjectile ("MVP_RifleBullet",8,flags:CMF_AIMDIRECTION,Pitch);
				HandleTargetPrediction (Proj,(0.4,-0.4),(0.55,-0.55),1024);
				Frame = UpdateTurretElevation (11,9,13,15); //L, J, N, P
			}
			APC2 # 1
			{
				//A_FaceTarget(0,0,flags:FAF_MIDDLE);
				//If you have no target, or it's dead.
				If (!Target || IsDead (Target))
				{
					A_ClearTarget();
					LookForPlayers (True); //Look for a new target.
				}
				
				//If you share a target with the autocannon, then try finding a different one.
				//Unless the shared target is powerful, in which case don't force yourself to attack a different target.
				If (!FocusFire && Target && Master && Target == Master.Target && !IsPowerful(Master.Target))
				{
					Actor NewTarget = PickDifferentTarget (Master.Target,Master.Target,2048,512);
					If (NewTarget)
						Target = NewTarget;
				}
				
				If (Random2[pr_monsterrefire]() < 128 &&
				((Target && IsOverPitchLimits (Target,12,Target.Height/2) != ELEVATION_INBOUNDS) ||
				!MVP_TurretCheckLOF (Target,150,0,(0,0,8))))
				{
					A_ClearTarget();
					Return ResolveState ("See");
				}
				
				HandleChaseTimer();
				
				Frame = UpdateTurretElevation (10,8,12,14); //K, I, M, O
				Return State (Null);
			}
			APC2 # 0 MVP_TurretRefire (128,"See",True);
			Goto Fire+6;
		Death:
			APC2 V 20
			{
				//Match angles with the autocannon, because this off-center turret rotating causes some sprite fuckery due to the nature of sprites.
				//It's not a visible visual bug in motion, since usually the APC is running all over the place and aiming at targets everywhere etc.
				//But it does become visible if you pause, or if the MG is killed while not having matching angle with its' autocannon.
				If (Master)
				{
					Angle = Master.Angle;
					TurretRotationOffset = 0; //Also remove the angle offset relative to the autocannon that was collected in Die().
				}
				RotateToVehicle (RTVF_NOTARGETCHECK);
				Actor Boom = Spawn ("MVP_Explosion",Pos);
				Boom.Scale *= 0.4;
				Boom.SetDamage (24);
				Boom.MaxTargetRange = 64;
				Boom.MeleeRange = 0;
				Boom.bNoGravity = True;
			}
			APC2 WWWWWWWWWWWWWW 2 RotateToVehicle (RTVF_NOTARGETCHECK);
			APC2 X 1 RotateToVehicle (RTVF_NOTARGETCHECK);
			Wait;
		XDeath:
			TNT1 A 0
			{
				Actor Boom = Spawn ("MVP_Explosion",Pos);
				Boom.Scale *= 0.75;
				Boom.SetDamage (48);
				Boom.MaxTargetRange = 96;
				Boom.MeleeRange = 24;
				Boom.bNoGravity = True;
			}
			Stop;
		GenericFreezeDeath:
			APC2 # 0 {Frame = UpdateTurretElevation (10,8,12,14); /*K, I, M, O*/}
			Goto Super::GenericFreezeDeath;
	}
}
Class MVP_50CalBullet : MVP_BaseProjectile
{
	Default
	{
		Radius 4;
		Height 8;
		Speed 230;
		FastSpeed 250;
		XScale 0.2;
		YScale 0.166667; //0.2 / 1.2
		DamageFunction (Random (60,80)/10); //A ripper damages an actor with a total radius of 64 MU, 10 times, hence dividing the random damage output by 10.
		ProjectileKickback 32;
		RipperLevel 3;
		Obituary "%o was gunned down by a machine gun";
		Decal "50CalChip";
		DamageType "MarineHMG";
		BounceSound "SFX/BulletRicochet";
		MVP_BaseProjectile.TrailActor "MVP_Tracer";
		MVP_BaseProjectile.InitialTrailsSkipped 6;
		MVP_BaseProjectile.MaxTrails 128;
		+Ripper;
	}
	Int RipAmount;
	Const Rips = 5; //Roughly how many actors it'll rip.
	Bool HitLiquid;
	
	Override Int SpecialMissileHit (Actor Victim)
	{
		If (!Victim || Victim && Victim == DontCollide) Return 1;
		If (Victim.bSolid && !Victim.bShootable) Return 1; //Don't stop at decorations that normally block projectiles.
		
		If (Target && !IsInanimateObject(Victim) && !MVP_IsHostile (Target,Victim)) Return 1; //Don't harm the shooter or allies.
		
		//Always bounce off players with god mode. Or player pawns and monsters with Invulnerable or NoDamage.
		If ((HasGodMode(Victim) || IsIndestructible(Victim) && !IsInanimateObject(Victim)) && DeflectProjectile (Victim,0,FRandom(0.4,0.6),FRandom (1.,0.7),(10,-10),(10,-10),attenuation:0.75)) {bRipper = False; Return 1;}
		//Fairly likely to bounce off healthy enough actors that don't bleed (Which should suggest its' something inorganic like a robot or golem or something, where deflection would make sense).
		Else If (Victim.bNoBlood && !IsIndestructible (Victim) && Victim.SpawnHealth() >= 2000 && DeflectProjectile (Victim,192,FRandom(0.5,0.7),FRandom (1.,0.85),(6,-6),(6,-6),attenuation:0.75)) {bRipper = False; Return 1;}
		
		If (Victim.SpawnHealth() <= 200) bExtremeDeath = True; //Obliterate any weak enemy.
		Else bExtremeDeath = False;
		
		//Stop penetrating if the target has a lot of health, or you ripped for long enough.
		If (Victim.SpawnHealth() >= 500 || RipAmount > Rips*10) bRipper = False;
		
		//Stop ripping if you went through a large enough ally.
		//If (Target && Target.bIsMonster && !(Target.IsHostile(Victim)) && Victim.Radius >= 40 && Victim.Height >= 64) A_Die();
		
		RipAmount++;
		
		If (!Victim.Player && Victim.PainChance <= 80 && Random (0,256) >= 128+48) bPainLess = True; //So monsters aren't frozen in pain from the amount of bullets hitting them.
		Return -1;
	}
	
	Void Do50BMGSplash (Int LiquidType, Bool UpsideDown = False)
	{
		If (LiquidType == LIQUID_NONE) Return;
		HitLiquid = True;
		Actor Splash = SpawnLiquidSplash (LiquidType,volume:0.4,1.0);
		
		Double Size = FRandom (0.3,0.7);
		Splash.Scale.X = Splash.Scale.Y = (UpsideDown ? -Size : Size);
		Splash.bXFlip = Random (False,True);
	}
	
	States
	{
		Spawn:
			MVP1 A -1 Light ("50CalTracerLight");
			Stop;
		Death: //Bullet hit level geometry.
			TNT1 A 1
			{
				//The floor was hit.
				If (Pos.Z <= FloorZ)
					Do50BMGSplash(CheckLiquid(FloorPic));
				//The ceiling was hit.
				Else If (Pos.Z + Height >= CeilingZ)
					Do50BMGSplash(CheckLiquid(CeilingPic),True);
				
				If (HitLiquid) Destroy(); //Splash was spawned, so just delete yourself.
				Else A_StartSound ("Vehicle/APC/MGImpact");
			}
			Stop;
		XDeath: //Bullet hit a bleeding actor.
			TNT1 A 1; //Don't play the impact sound on bleeding actors.
			Stop;
		Crash: //Bullet hit a non bleeding actor.
			TNT1 A 1 A_StartSound ("Vehicle/APC/MGImpact");
			Stop;
	}
}

//A smaller bullet projectile sort of based on the 7.62mm NATO round.
Class MVP_RifleBullet : MVP_BaseProjectile
{
	Default
	{
		Radius 4;
		Height 6;
		Speed 200; //IRL, the M240 has a slightly slower muzzle velocity than the M2 Browning, so the rifle round is slightlky slower too, but not as slow as the autocannon.
		//I mean, autocannon shells are also fired at a higher muzzle velocity than bullets, yet the autocannon projectile is SLOWER instead of faster, but whatever.
		FastSpeed 220;
		DamageFunction (Random (10,16)); //The marines' rifles do 6 to 10 damage, so this is about double that. Why ? I don't fucking know, let's just say the marines use 5.62mm rounds or something.
		XScale 0.1;
		YScale 0.083333; //0.1 / 1.2
		ProjectileKickback 16;
		Decal "BulletChip";
		DamageType "RifleBullet";
		Obituary "%o found out that rifle rounds are more dangerous than in the movies";
		BounceSound "SFX/BulletRicochet";
		MVP_BaseProjectile.TrailActor "MVP_Tracer";
		MVP_BaseProjectile.InitialTrailsSkipped 3;
		MVP_BaseProjectile.MaxTrails 64;
		MVP_BaseProjectile.TrailLineSpacing 1;
	}
	
	Bool HitLiquid;
	
	Void DoRifleSplash (Int LiquidType, Bool UpsideDown = False)
	{
		If (LiquidType == LIQUID_NONE) Return;
		HitLiquid = True;
		Actor Splash = SpawnLiquidSplash (LiquidType,"SFX/WaterHit",1.0,1.0);
		
		Double Size = FRandom (0.2,0.3);
		Splash.Scale.X = Splash.Scale.Y = (UpsideDown ? -Size : Size);
		Splash.bXFlip = Random (False,True);
	}
	
	Override Void TrailSpawn (Actor Trail)
	{
		Super.TrailSpawn (Trail);
		
		Trail.Scale.X = Trail.Scale.Y = 0.25;
	}
	
	Override Int SpecialMissileHit (Actor Victim)
	{
		If (!Victim || Victim && Victim == DontCollide) Return 1;
		If (Target && !IsInanimateObject(Victim) && !MVP_IsHostile (Target,Victim)) Return 1; //Don't harm the shooter or allies.
		
		//Always bounce off players with god mode. Or player pawns and monsters with Invulnerable or NoDamage.
		If ((HasGodMode(Victim) || IsIndestructible(Victim) && !IsInanimateObject(Victim)) && DeflectProjectile (Victim,0,FRandom(0.4,0.6),FRandom (1.,0.7),(10,-10),(10,-10))) Return 1;
		//Be at least somewhat likely to bounce off if hitting an APC.
		Else If ((Victim Is "MVP_APC" || Victim Is "MVP_APCProp") && DeflectProjectile (Victim,128,FRandom(0.4,0.6),FRandom (1.,0.7),(10,-10),(10,-10))) Return 1;
		//Be very likely to bounce off a non-bleeding actor with a lot of health.
		Else If (Victim.bNoBlood && !IsIndestructible (Victim) && Victim.SpawnHealth() >= 2000 && DeflectProjectile (Victim,64,FRandom(0.5,0.7),FRandom (1.,0.8),(6,-6),(6,-6),attenuation:0.75)) {bRipper = False; Return 1;}
		
		
		Return -1;
	}
	
	//Mom, can we have MVP_50CalBullet ?
	//No, we have MVP_50CalBullet at home.
	//The MVP_50CalBullet at home:
	States
	{
		Spawn:
			MVP1 A -1 Light ("RifleTracerLight");
			Stop;
		Death: //Bullet hit level geometry.
			TNT1 A 1
			{
				//The floor was hit.
				If (Pos.Z <= FloorZ)
					DoRifleSplash(CheckLiquid(FloorPic));
				//The ceiling was hit.
				Else If (Pos.Z + Height >= CeilingZ)
					DoRifleSplash(CheckLiquid(CeilingPic),True);
				
				If (HitLiquid) Destroy(); //Splash was spawned, so just delete yourself.
				Else A_StartSound ("Vehicle/APC/SmallMGImpact");
			}
			Stop;
		XDeath: //Bullet hit a bleeding actor.
			TNT1 A 1; //Don't play the impact sound on bleeding actors.
			Stop;
		Crash: //Bullet hit a non bleeding actor.
			TNT1 A 1 A_StartSound ("Vehicle/APC/SmallMGImpact");
			Stop;
	}
}

Class MVP_AutocannonShell : MVP_BaseProjectile
{
	Default
	{
		Radius 12;
		Height 16;
		Speed 180;
		FastSpeed 200;
		XScale 0.5;
		YScale 0.416666; //0.5 / 1.2
		DamageFunction (Random (60,80));
		ProjectileKickback 128;
		Obituary "%o took a 30mm round to the face";
		Decal "30mmScorch";
		DamageType "Autocannon";
		MVP_BaseProjectile.TrailActor "MVP_Tracer";
		MVP_BaseProjectile.TrailDelay 1;
		MVP_BaseProjectile.InitialTrailsSkipped 16;
		MVP_BaseProjectile.TrailFadeAndScaleStart 64;
		MVP_BaseProjectile.TrailLineSpacing 4;
		MVP_BaseProjectile.MaxTrails 128;
		MVP_BaseProjectile.ScaleAmountX -0.05;
		MVP_BaseProjectile.ScaleAmountY -0.05;
		MVP_BaseProjectile.FadeAmount -0.05;
	}
	
	Override Void TrailSpawn (Actor Trail)
	{
		Super.TrailSpawn (Trail);
		
		Trail.Scale.X = Trail.Scale.Y = 1.1;
	}
	
	Override Void Tick()
	{
		Super.Tick();
		If (IsFrozen()) Return;
		
		//If you are an airburst shell and have a specific target.
		If (IsAirburst && Tracer && bMissile) //bMissile is removed when projectiles explode. Making this check not run on exploded shells.
		{
			//And are within blast range of the tracer.
			If (Tracer.Health > 0 && Distance3DSquared (Tracer) <= 176*176)
			{
				FLineTraceData TracerCheck;
				
				LineTrace (Angle,192,Pitch,TRF_THRUHITSCAN|TRF_SOLIDACTORS|TRF_BLOCKSELF,data:TracerCheck);
				
				//If you aren't going to directly impact the tracer.
				If (TracerCheck.HitActor != Tracer)
					SmokeCounter++; //Give it another chance in the next tick.
				Else
					SmokeCounter = 0;
				
				//If you still haven't lined up to hit the tracer. Then airburst.
				If (SmokeCounter > 1)
				{
					AirBursted = True;
					ExplodeMissile();
				}
			}
		}
	}
	
	Override Int SpecialMissileHit (Actor Victim)
	{
		If (!Victim) Return 1;
		If (Target && !IsInanimateObject(Victim) && !MVP_IsHostile (Target,Victim)) Return 1; //Don't harm the shooter or allies.
		
		If (Victim.SpawnHealth() <= 400) bExtremeDeath = True; //Always gib weak enough enemies.
		Else bExtremeDeath = False;
		
		Return -1;
	}
	
	Void DoAutocannonSplash (Int LiquidType, Bool UpsideDown = False)
	{
		If (LiquidType == LIQUID_NONE) Return;
		HitLiquid = True;
		Actor Splash = SpawnLiquidSplash (LiquidType,explosiondamage:96,48,0,'Explosive', True);
		
		Double Size = FRandom (1.8,2.2);
		Splash.Scale.X = Splash.Scale.Y = (UpsideDown ? -Size : Size);
		Splash.bXFlip = Random (False,True);
	}
	
	Bool HitLiquid; //Removes the autocannon shell if it hit a liquid texture.
	Bool IsAirburst; //Should the shell prematurely explode if it's within 150 map units of its' tracer ?
	Bool Airbursted; //Did the shell explode in an airburst ? Ensures that airbursted shells ALWAYS go to the AirBlast state.
	
	States
	{
		Spawn:
			MVP1 C 0;
			MVP1 # 4 Light ("AutocannonTracer")
			{
				A_FaceMovementDirection ();
				Frame = UpdateProjectileElevation (5,6,2,3,4); //F, G, C, D, E
			}
			Wait;
		Death: //Shell hit level geometry.
			TNT1 A 0
			{
				If (Airbursted) //The shell detonated mid air to hit a target that it missed.
				{
					A_SetScale (1.5,1.5);
					A_SetRenderStyle (1.0,Style_Add);
					Return ResolveState ("AirBlast");
				}
				
				//The floor was hit.
				If (Pos.Z <= FloorZ)
					DoAutocannonSplash(CheckLiquid(FloorPic));
				//The ceiling was hit.
				Else If (Pos.Z + Height >= CeilingZ)
					DoAutocannonSplash(CheckLiquid(CeilingPic),True);
				
				If (HitLiquid) Destroy(); //Splash was spawned, so just delete yourself.
				
				Return State (Null);
			}
			TNT1 A 0
			{
				A_SetScale (1.5,1.5);
				A_SetRenderStyle (1.0,Style_Add);
				If (Pos.Z + Height >= CeilingZ) Scale.Y = -1.5; //If a ceiling was hit, invert the explosion.
				Else If (Pos.Z >= FloorZ+24) Return ResolveState ("AirBlast"); //If the explosion happened above 24 map units from the ground, trigger the air explosion.
				A_AttachLight ("ExplosionLight",DynamicLight.PulseLight,"FFE2B3",512,0,DYNAMICLIGHT.LF_ATTENUATE,(0,0,1),param:1.714285714285714*3.4); //Only for ground explosions !
				//SetOrigin ((Pos.X,Pos.Z,FloorZ),False); //Otherwise stick to the ground to explode if you didn't hit level geometry too far above the ground.
				Return State (Null);
			}
			GXPL A 2 Bright;
			GXPL B 4 Bright
			{
				A_StartSound ("Vehicle/APC/AutocannonHit",attenuation:0.3);
				MakeAlliesImmuneToExplosion (128);
				A_Explode (96,128,XF_EXPLICITDAMAGETYPE,True,32,damagetype:'Explosive');
				RemoveAllyExplosionImmunity();
			}
			GXPL CDEFGHIJKLMNO 4 Bright;
			Stop;
		XDeath: //Shell hit a bleeding actor.
			Goto AirBlast;
		Crash: //Shell hit a non bleeding actor.
			Goto AirBlast;
		AirBlast:
			TNT1 A 0 //Stop moving in the air, and add some random rotation for variety.
			{
				A_StartSound ("Vehicle/APC/AutocannonHit",attenuation:0.15); //Air explosions makes sound that travels further.
				A_Stop();
				A_SetRenderStyle (1.0,Style_Add);
				A_SetScale (1.5,1.5);
				Gravity = 0;
				bRollSprite = True;
				Roll = FRandom (0,180);
			}
			GAXP A 4 Bright;
			GAXP B 4 Bright
			{
				MakeAlliesImmuneToExplosion (128);
				A_Explode (96,192,XF_EXPLICITDAMAGETYPE,True,48,damagetype:'Explosive');
				RemoveAllyExplosionImmunity();
			}
			GAXP CDEFGHIJ 4 Bright;
			Stop;
	}
}

Class MVP_DestroyedMGTurretGun : Actor
{
	Default
	{
		Radius 16;
		Height 32;
		XScale 0.65;
		YScale 0.541666;
		Speed 48;
		Mass 200;
		Gravity 1;
		ProjectileKickback 48;
		DamageFunction (Random (64,96));
		DamageType "Melee";
		Obituary "%o was hit by a destroyed machine gun";
		Projectile;
		-NoBlockmap;
		-NoGravity;
		+BloodlessImpact;
	}
	
	States
	{
		Spawn:
			TNT1 A 0;
			APC3 O 1
			{
				//The turret leans upwards over time as it falls until impact.
				Switch (GetAge())
				{
					Case 35/2:
						Frame = 15;
						Break;
					Case 35:
						Frame = 16;
						Break;
					Case 50:
						Frame = 17;
						Break;
					Default:
						Break;
				}
			}
			Loop;
		Death:
			APC3 O 0 A_StartSound ("SFX/MetalHit",volume:0.75,0.5);
			APC3 O -1;
			Stop;
	}
}

Class MVP_DestroyedAPCFrontDoor : MVP_DestroyedMGTurretGun
{
	Default
	{
		Radius 12;
		Height 48;
		Speed 48;
		Mass 100;
		DamageFunction (Random (48,64));
		ProjectileKickback 16;
		Obituary "%o was hit by an APC's door";
	}
	
	Override Void Tick()
	{
		Super.Tick();
		
		If (IsFrozen ()) Return;
		//Slow down the spinning over time.
		If (ReactionTime <= 24 && GetAge() % 8 == 0)
			ReactionTime++;
	}
	
	States
	{
		Spawn:
			APC5 ABCD 2 A_SetTics (ReactionTime);
			Loop;
		Death:
			APC5 E -1 A_StartSound ("SFX/MetalHit",volume:0.5);
			Stop;
	}
}

Class MVP_APCHatch : MVP_DestroyedAPCFrontDoor
{
	Default
	{
		Radius 12;
		Height 8;
		ProjectileKickBack 8;
		DamageFunction (Random (12,24));
		Obituary "%o had an APC hatch fall from the sky";
	}
	
	States
	{
		Spawn:
			APC2 YZ[] 2 A_SetTics (ReactionTime);
			Loop;
		Death:
			APC2 Y -1 A_StartSound ("SFX/MetalHit",volume:0.5);
			Stop;
	}
}
#Include "Sounds/Vehicles/Army Car/Pain Ray/e"
Class MVP_APCWheel : MVP_DestroyedMGTurretGun
{
	Default
	{
		Radius 24;
		Height 32;
		Speed 48;
		Mass 100;
		ProjectileKickback 24;
		DamageFunction (Random (16,32));
		BounceType "Hexen";
		BounceFactor 0.75;
		BounceSound "SFX/WheelHit";
		Obituary "%o was smacked by an APC wheel";
		+CanBounceWater;
		+DontBounceOnSky;
		+BounceOnActors;
	}
	
	Override Void Tick()
	{
		Actor.Tick();
		
		If (IsFrozen() || InStateSequence(CurState,ResolveState ("Death"))) Return;
		
		//Slow down the spinning over time.
		If (ReactionTime <= 24 && GetAge() % 8 == 0)
			ReactionTime++;
		
		//Stop moving if too slow.
		If (Vel.Length() >= 2)
			BounceSound = Default.BounceSound;
		Else
		{
			BounceSound = "";
			ExplodeMissile(); //Hit the ground.
		}
	}
	States
	{
		Spawn:
			APC5 FGHI 2 A_SetTics (ReactionTime);
			Loop;
		Death:
			APC5 J -1;
			Stop;
	}
}

//Grotesque Zandronum-era hack.
Class MVP_DesolidifyToken : Inventory
{
	Default
	{
		ReactionTime 8; //Tic timer before the item leaves.
		+Inventory.Untossable;
	}
	
	Override Void AttachToOwner(Actor Other)
	{
		Super.AttachToOwner(Other);
		
		If (Other)
			Other.bSolid = False;
	}
	
	Override Void DoEffect()
	{
		If (!ReactionTime)
			GoAwayAndDie();
		If (ReactionTime)
			ReactionTime--;
	}
	
	Override Void DetachFromOwner()
	{
		Super.DetachFromOwner();
		If (Owner)
			Owner.bSolid = True;
	}
}

//An alias for the APC, which can be used to spawn the APC through the console with the machine gun included.
Class MVP_APC_MG : MVP_APC
{
	//Spawn only the MG.
	Override Void SpawnVehicleTurret ()
	{
		Turret = MVP_BaseTurret(Spawn ("MVP_APCMachineGun",Pos+TurretOffsets));
		If (Turret)
		{
			Turret.CopyFriendliness (Self,False);
			Turret.Angle = Angle;
			Turret.Pitch = Pitch;
			Turret.Master = Self;
			Turret.TurretOffsets = TurretOffsets;
			User_Turret = "Machine Gun";
			SetTag ("Armored Personnel Carrier (Machine Gun)");
		}
	}
}

Class MVP_APC_Autocannon : MVP_APC
{
	//Spawn only the autocannon.
	Override Void SpawnVehicleTurret ()
	{
		Turret = MVP_BaseTurret(Spawn ("MVP_APCAutocannon",Pos+TurretOffsets));
		If (Turret)
		{
			Turret.CopyFriendliness (Self,False);
			Turret.Angle = Angle;
			Turret.Pitch = Pitch;
			Turret.Master = Self;
			Turret.TurretOffsets = TurretOffsets;
			User_Turret = "Autocannon";
			SetTag ("Armored Personnel Carrier (Autocannon)");
		}
	}
}