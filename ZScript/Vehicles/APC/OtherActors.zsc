//$GZDB_SKIP
Class MVP_APCMachineGun : MVP_BaseTurret
{
	Default
	{
		Health 400;
		GibHealth 250;
		Height 32;
		DeathHeight 32;
		Radius 32;
		Mass 700;
		FriendlySeeBlocks 80; //A bit over 10000 map unit sight range, useful for vehicles with actual guns to attack with.
		MVP_BaseTurret.MaximumPitch -70;
		MVP_BaseTurret.MinimumPitch 20;
		Tag "Machine Gun Turret";
		Obituary "%o was gunned down by a machine gun turret";
		DamageFactor "Autocannon", 0.8;
		+MissileMore;
		+MVP_BaseVehicle.ScareMarines;
	}
	Override Bool ShouldAttack ()
	{
		If (!Target) Return False;
		
		If (Target != Self && MVP_CheckMissileRange(Target) && TurretLOFCheck (12,ignoreactors:True) && !IsDead (Target) && IsHostile (Target))
		{
			SetStateLabel ("Fire");
			Return True;
		}
		
		Return False;
	}
	
	Override Void PostBeginPlay ()
	{
		Super.PostBeginPlay();
		
		MissileState = FindState ("Fire");
	}
	
	Override Void Tick()
	{
		Double OldAngle = Angle;
		Super.Tick();
		
		If (IsFrozen()) Return;
		
		If (AbsAngle (Angle,OldAngle) >= 2 && LastEnemyDelay)
			A_StartSound ("Vehicle/APC/TurretTurn",attenuation:0.8);
	}
	
	Override Void Die (Actor Source, Actor Inflictor, Int DmgFlags, Name MeansOfDeath)
	{
		Super.Die (Source, Inflictor, DmgFlags, MeansOfDeath);
		If (Master) Master.SetTag (Master.Default.GetTag());
	}
	
	Override Void OnDestroy()
	{
		If (Master) Master.SetTag (Master.Default.GetTag());
		Super.OnDestroy();
	}
	
	States
	{
		Spawn:
			APC3 A 0;
			APC3 # 1
			{
				RotateToVehicle();
				A_LookEx (maxseedist:DEFAULT_SEEDISTANCE,DEFAULT_HEARDISTANCE);
				Frame = UpdateElevation (3,0,6,9); //D, A, G, J
			}
			Loop;
		See:
			APC3 # 1
			{
				A_FaceTarget (5,5,flags:FAF_MIDDLE);
				RotateToVehicle();
				A_Chase (Null,Null,CHF_DONTIDLE|CHF_DONTMOVE|CHF_NODIRECTIONTURN);
				MVP_Chase (NoChase:True);
				UpdateLastEnemyTimer();
				Frame = UpdateElevation (3,0,6,9); //D, A, G, J
			}
			Loop;
		Fire:
			APC3 ###### 1
			{
				A_FaceTarget (5,5,flags:FAF_MIDDLE);
				Frame = UpdateElevation (3,0,6,9); //D, A, G, J
			}
			APC3 # 3 Light ("50CalFiringLight") Bright //Right shot
			{
				A_FaceTarget (5,5,flags:FAF_MIDDLE);
				A_StartSound ("Vehicle/APC/MGFire",CHAN_WEAPON,CHANF_OVERLAP,1.0,0.3);
				Actor Proj = A_SpawnProjectile ("MVP_50CalBullet",12,-8,flags:CMF_AIMDIRECTION,Pitch); //pew pew
				HandleTargetPrediction (Proj,(0.8,-0.8),(1.0,-1.0),1280);
				Frame = UpdateElevation (4,1,7,10); //E, B, H, K
			}
			APC3 # 3 Light ("50CalFiringLight") Bright //Left shot
			{
				A_FaceTarget (5,5,flags:FAF_MIDDLE);
				A_StartSound ("Vehicle/APC/MGFire",CHAN_WEAPON,CHANF_OVERLAP,1.0,0.3);
				Actor Proj = A_SpawnProjectile ("MVP_50CalBullet",12,8,flags:CMF_AIMDIRECTION,Pitch); //pew pew
				HandleTargetPrediction (Proj,(0.8,-0.8),(1.0,-1.0),1280);
				Frame = UpdateElevation (5,2,8,11); //F, C, I, L
			}
			APC3 # 1
			{
				//If you have no target, or it's dead, or it's too high up or low doown for you too shoot.
				If (!Target || IsDead (Target) || Pitch < MaxPitch || Pitch > MaxPitch)
				{
					A_ClearTarget();
					LookForPlayers (True); //Look for a new target.
				}
				
				If (!TurretLOFCheck (12,ignoreactors:True))
				{
					A_ClearTarget();
					Return ResolveState ("See");
				}
				
				HandleChaseTimer();
				
				Frame = UpdateElevation (3,0,6,9); //D, A, G, J
				Return State (Null);
			}
			APC3 # 0 A_MonsterRefire (128,"See");
			Goto Fire+6;
		Death:
			APC3 M 0 {Actor Boom = Spawn ("MVP_Explosion",Pos); Boom.bNoGravity = True;}
			APC3 M 1 RotateToVehicle(True);
			Wait;
		XDeath:
			APC3 N 0
			{
				Height = 16;
				Actor Boom = Spawn ("MVP_Explosion",Pos);
				Boom.Scale *= 1.75;
				Boom.SetDamage (192);
				Boom.MeleeRange = 64;
				Boom.bNoGravity = True;
				
				Actor Flame = Spawn ("MVP_Flame",Pos);
				MVP_Flame(Flame).Offsets = (0.1,0,4);
				Flame.Scale.X = Flame.Scale.Y = 1.0;
				Flame.Master = Self;
				Flame.ReactionTime *= FRandom (1.25,4.25);
				Flame.SetDamage (8);
				Flame.MaxTargetRange = 64;
				Flame.MeleeRange = 32;
				
				A_SpawnItemEx ("MVP_DestroyedMGTurretGun",16,0,12,FRandom (16,32),FRandom (-6,6),FRandom (2,6));
			}
			APC3 N 1 RotateToVehicle(True);
			Wait;
		GenericFreezeDeath:
			APC3 # 0 {Frame = UpdateElevation (3,0,6,9); /*D, A, G, J*/}
			Goto Super::GenericFreezeDeath;
	}
}

//Somewhat based on the MG.
Class MVP_APCAutocannon : MVP_APCMachineGun
{
	Default
	{
		Health 450;
		GibHealth 300;
		Height 20;
		DeathHeight 20;
		Mass 1000;
		Tag "Autocannon Turret";
		Obituary "%o was blown up by an autocannon";
		MVP_BaseVehicle.VehicleTurret "MVP_APCMGTurret";
		+MissileEvenMore;
	}
	
	Bool FocusFire; //Turned on by the APC, then passed over to the small MG on the autocannon.
	
	Override Void Tick()
	{
		Double OldAngle = Angle;
		MVP_BaseTurret.Tick();
		
		If (IsFrozen()) Return;
		
		If (AbsAngle (Angle,OldAngle) >= 4 && LastEnemyDelay)
			A_StartSound ("Vehicle/APC/TurretTurn",attenuation:0.8);
	}
	
	Override Void PostBeginPlay ()
	{
		Super.PostBeginPlay();
		
		MissileState = FindState ("Fire");
	}
	
	Override Void SpawnVehicleTurret ()
	{
		Vector3 Original = TurretOffsets;
		TurretOffsets = (-0.1,0,1.1); //Temporarily set the autocannons' turret offsets to this, for spawning the small MG, then revert to the previous offsets.
		Super.SpawnVehicleTurret();
		If (FocusFire && Turret) MVP_APCMGTurret(Turret).FocusFire = True;
		If (bNoTargetPrediction) Turret.bNoTargetPrediction = True;
		TurretOffsets = Original;
	}
	
	Override Bool ShouldAttack ()
	{
		If (!Target) Return False;
		
		If (Target != Self && MVP_CheckMissileRange(Target) && TurretLOFCheck (12) && !IsDead (Target) && IsHostile (Target))
		{
			SetStateLabel ("Fire");
			Return True;
		}
		
		Return False;
	}
	
	States
	{
		Spawn:
			APC2 A 0;
			APC2 # 1
			{
				RotateToVehicle();
				A_LookEx (maxseedist:DEFAULT_SEEDISTANCE,DEFAULT_HEARDISTANCE);
				Frame = UpdateElevation (2,0,4,6); //C, A, E, G
			}
			Loop;
		See:
			APC2 # 1
			{
				A_FaceTarget (5,5,flags:FAF_MIDDLE);
				RotateToVehicle();
				A_Chase (Null,Null,CHF_DONTIDLE|CHF_DONTMOVE|CHF_NODIRECTIONTURN);
				MVP_Chase (NoChase:True);
				UpdateLastEnemyTimer();
				Frame = UpdateElevation (2,0,4,6); //C, A, E, G
			}
			Loop;
		Fire:
			APC2 ###### 1
			{
				A_FaceTarget (5,5,flags:FAF_MIDDLE);
				Frame = UpdateElevation (2,0,4,6); //C, A, E, G
			}
			APC2 # 3 Light ("AutocannonFlash") Bright
			{
				A_FaceTarget (5,5,flags:FAF_MIDDLE);
				A_StartSound ("Vehicle/APC/AutocannonFire",CHAN_WEAPON,CHANF_OVERLAP,1.0,0.25);
				Actor Proj = A_SpawnProjectile ("MVP_AutocannonShell",16,flags:CMF_AIMDIRECTION,Pitch); //pew pew
				HandleTargetPrediction (Proj,(0.3,-0.3),(0.45,-0.45),768);
				Frame = UpdateElevation (3,1,5,7); //D, B, F, H
			}
			APC2 ### 8
			{
				//If you have no target, or it's dead, or it's too high up or low doown for you too shoot.
				If (!Target || IsDead (Target) || Pitch < MaxPitch || Pitch > MaxPitch)
				{
					A_ClearTarget();
					LookForPlayers (True); //Look for a new target.
				}
				
				If (!TurretLOFCheck())
				{
					A_ClearTarget();
					Return ResolveState ("See");
				}
				
				HandleChaseTimer();
				
				Frame = UpdateElevation (2,0,4,6); //C, A, E, G
				Return State (Null);
			}
			APC2 # 0 A_MonsterRefire (10,"See");
			Goto Fire+6;
		Death: //When TG5 finishes the sprites for the gunner getting out the turret, add code to make them work here.
			APC2 Q 0
			{
				Actor Boom = Spawn ("MVP_Explosion",Pos);
				Boom.Scale *= 1.75;
				Boom.SetDamage (192);
				Boom.MeleeRange = 64;
				Boom.bNoGravity = True;
			}
			APC2 Q 1 RotateToVehicle (True);
			Wait;
		XDeath:
			APC2 U 0
			{
				Height = 16;
				Actor Boom = Spawn ("MVP_Explosion",Pos);
				Boom.Scale *= 2.5;
				Boom.SetDamage (256);
				Boom.MeleeRange = 96;
				Boom.MaxTargetRange = 384;
				Boom.bNoGravity = True;
				
				Actor Flame = Spawn ("MVP_Flame",Pos);
				MVP_Flame(Flame).Offsets = (0.1,0,4);
				Flame.Scale.X = Flame.Scale.Y = 1.0;
				Flame.Master = Self;
				Flame.ReactionTime *= FRandom (1.25,4.25);
				Flame.SetDamage (8);
				Flame.MaxTargetRange = 64;
				Flame.MeleeRange = 32;
				
				A_SpawnItemEx ("MVP_APCHatch",-12,6,24,FRandom (6,-6),FRandom (6,-6),FRandom (18,24));
			}
			APC2 U 1 RotateToVehicle(True);
			Wait;
		GenericFreezeDeath:
			APC2 # 0 {Frame = UpdateElevation (2,0,4,6); /*C, A, E, G*/}
			Goto Super::GenericFreezeDeath;
	}
}

//The smaller MG turret that is attached on the autocannon.
Class MVP_APCMGTurret : MVP_BaseTurret
{
	Default
	{
		Health 125;
		GibHealth 60;
		Height 12;
		DeathHeight 12;
		Radius 24;
		Mass 150;
		RipLevelMin 1;
		MaxTargetRange 6144; //Reduced range compared to most attacks.
		FriendlySeeBlocks 48; //Reduced sight range too.
		MVP_BaseTurret.MaximumPitch -60; //Doesn't elevator quite as high as the other APC turrets.
		MVP_BaseTurret.MinimumPitch 20;
		Obituary "%o found out that rifle rounds are more dangerous than in the movies";
		Tag "7.62mm Turret";
		DamageType "RifleBullet";
		DamageFactor "Autocannon", 0.95;
		DamageFactor "Melee", 0.4;
		DamageFactor "Electric", 0.4;
		DamageFactor "ExplosiveImpact", 0.825;
		DamageFactor "Explosion", 0.825;
		DamageFactor "HitScan", 0.9;
		+MissileMore;
		+MissileEvenMore;
	}
	
	Bool FocusFire; //Makes the MG not actively avoid sharing the same target as the turret, even if the shared target isn't a powerful enemy.
	
	Override Void PostBeginPlay ()
	{
		Super.PostBeginPlay();
		
		MissileState = FindState ("Fire");
	}
	
	Override Bool ShouldAttack ()
	{
		If (!Target) Return False;
		
		If (Target != Self && MVP_CheckMissileRange(Target) && TurretLOFCheck (8,150) && !IsDead (Target) && IsHostile (Target))
		{
			SetStateLabel ("Fire");
			Return True;
		}
		
		Return False;
	}
	States
	{
		Spawn:
			APC2 I 0;
			APC2 # 1
			{
				RotateToVehicle();
				A_LookEx (maxseedist:MaxTargetRange,DEFAULT_HEARDISTANCE);
				Frame = UpdateElevation (10,8,12,14); //K, I, M, O
			}
			Loop;
		See:
			APC2 # 1
			{
				A_FaceTarget (6,6,flags:FAF_MIDDLE);
				RotateToVehicle();
				A_Chase (Null,Null,CHF_DONTIDLE|CHF_DONTMOVE|CHF_NODIRECTIONTURN);
				MVP_Chase (NoChase:True);
				UpdateLastEnemyTimer();
				Frame = UpdateElevation (10,8,12,14); //K, I, M, O
			}
			Loop;
		Fire:
			APC2 ###### 1
			{
				A_FaceTarget (5,5,flags:FAF_MIDDLE);
				Frame = UpdateElevation (10,8,12,14); //K, I, M, O
			}
			APC2 # 4 Light ("RifleFiringLight") Bright
			{
				A_FaceTarget (5,5,flags:FAF_MIDDLE);
				
				//Play at half the volume if the autocannon is playing its' firing sound.
				If (!Master)
					A_StartSound ("Vehicle/APC/SmallMGFire",CHAN_WEAPON,CHANF_OVERLAP,1.0,0.4);
				Else If (Master && Master.IsActorPlayingSound (CHAN_WEAPON))
					A_StartSound ("Vehicle/APC/SmallMGFire",CHAN_WEAPON,CHANF_OVERLAP,0.75,0.4);
				
				Actor Proj = A_SpawnProjectile ("MVP_RifleBullet",8,flags:CMF_AIMDIRECTION,Pitch);
				HandleTargetPrediction (Proj,(0.4,-0.4),(0.55,-0.55),1024);
				Frame = UpdateElevation (11,9,13,15); //L, J, N, P
			}
			APC2 # 1
			{
				//If you have no target, or it's dead, or it's too high up or low doown for you too shoot.
				If (!Target || IsDead (Target) || Pitch < MaxPitch || Pitch > MaxPitch || Master && Target == Master.Target)
				{
					A_ClearTarget();
					LookForPlayers (True); //Look for a new target.
				}
				
				//If you share a target with the autocannon, then try finding a different one.
				//Unless the shared target is powerful, in which case don't force yourself to attack a different target.
				If (!FocusFire && Target && Master && Target == Master.Target && !IsPowerful(Master.Target))
				{
					Actor NewTarget = PickDifferentTarget (Master.Target,Master.Target,2048,512);
					If (NewTarget)
						Target = NewTarget;
				}
				
				If (!TurretLOFCheck (8,150))
				{
					A_ClearTarget();
					Return ResolveState ("See");
				}
				
				HandleChaseTimer();
				
				Frame = UpdateElevation (10,8,12,14); //K, I, M, O
				Return State (Null);
			}
			APC2 # 0 A_MonsterRefire (128,"See");
			Goto Fire+6;
		Death:
			APC2 V 20
			{
				//Match angles with the autocannon, because this off-center turret rotating causes some sprite fuckery due to the nature of sprites.
				//It's not a visible visual bug in motion, since usually the APC is running all over the place and aiming at targets everywhere etc.
				//But it does become visible if you pause, or if the MG is killed while not having matching angle with its' autocannon.
				If (Master)
				{
					Angle = Master.Angle;
					DeathAngleOffset = 0; //Also remove the angle offset relative to the autocannon that was collected in Die().
				}
				RotateToVehicle (True);
				Actor Boom = Spawn ("MVP_Explosion",Pos);
				Boom.Scale *= 0.4;
				Boom.SetDamage (24);
				Boom.MaxTargetRange = 64;
				Boom.MeleeRange = 0;
				Boom.bNoGravity = True;
			}
			APC2 WWWWWWWWWWWWWW 2 RotateToVehicle (True);
			APC2 X 1 RotateToVehicle (True);
			Wait;
		XDeath:
			TNT1 A 0
			{
				Actor Boom = Spawn ("MVP_Explosion",Pos);
				Boom.Scale *= 0.75;
				Boom.SetDamage (48);
				Boom.MaxTargetRange = 96;
				Boom.MeleeRange = 24;
				Boom.bNoGravity = True;
			}
			Stop;
		GenericFreezeDeath:
			APC2 # 0 {Frame = UpdateElevation (10,8,12,14); /*K, I, M, O*/}
			Goto Super::GenericFreezeDeath;
	}
}
Class MVP_50CalBullet : MVP_BaseProjectile
{
	Default
	{
		Radius 4;
		Height 8;
		Speed 230;
		FastSpeed 250;
		XScale 0.2;
		YScale 0.166667; //0.2 / 1.2
		DamageFunction (Random (60,80)/10); //A ripper damages an actor with a total radius of 64 MU, 10 times, hence dividing the random damage output by 10.
		ProjectileKickback 32;
		RipperLevel 3;
		Obituary "%o was gunned down by a machine gun";
		Decal "50CalChip";
		DamageType "MarineHMG";
		BounceSound "SFX/BulletRicochet";
		MVP_BaseProjectile.TrailActor "MVP_Tracer";
		MVP_BaseProjectile.InitialTrailsSkipped 6;
		MVP_BaseProjectile.MaxTrails 128;
		+Ripper;
	}
	Int RipAmount;
	Const Rips = 5; //Roughly how many actors it'll rip.
	Bool HitLiquid;
	
	Override Int SpecialMissileHit (Actor Victim)
	{
		If (!Victim || Victim && Victim == DontCollide) Return 1;
		If (Victim.bSolid && !Victim.bShootable) Return 1; //Don't stop at decorations that normally block projectiles.
		
		If (Target && (Victim.bIsMonster || Victim.Player) && !(Target.IsHostile (Victim))) Return 1; //Don't harm the shooter or allies.
		
		//Always bounce off players with god mode. Or player pawns and monsters with Invulnerable or NoDamage.
		If ((HasGodMode(Victim) || IsIndestructible(Victim) && (Victim.Player || Victim.bIsMonster)) && DeflectProjectile (Victim,0,FRandom(0.4,0.6),FRandom (1.,0.7),(10,-10),(10,-10),attenuation:0.75)) {bRipper = False; Return 1;}
		//Fairly likely to bounce off healthy enough actors that don't bleed (Which should suggest its' something inorganic like a robot or golem or something, where deflection would make sense).
		Else If (Victim.bNoBlood && !IsIndestructible (Victim) && Victim.SpawnHealth() >= 2000 && DeflectProjectile (Victim,192,FRandom(0.5,0.7),FRandom (1.,0.85),(6,-6),(6,-6),attenuation:0.75)) {bRipper = False; Return 1;}
		
		If (Victim.SpawnHealth() <= 200) bExtremeDeath = True; //Obliterate any weak enemy.
		Else bExtremeDeath = False;
		
		//Stop penetrating if the target has a lot of health, or you ripped for long enough.
		If (Victim.SpawnHealth() >= 500 || RipAmount > Rips*10) bRipper = False;
		
		//Stop ripping if you went through a large enough ally.
		//If (Target && Target.bIsMonster && !(Target.IsHostile(Victim)) && Victim.Radius >= 40 && Victim.Height >= 64) A_Die();
		
		RipAmount++;
		
		If (!Victim.Player && Victim.PainChance <= 80 && Random (0,256) >= 128+48) bPainLess = True; //So monsters aren't frozen in pain from the amount of bullets hitting them.
		Return -1;
	}
	
	Void Do50BMGSplash (Int LiquidType, Bool UpsideDown = False)
	{
		HitLiquid = True;
		Actor Splash = SpawnLiquidSplash (LiquidType,volume:0.4,1.0);
		
		Splash.Scale.X = Splash.Scale.Y = (UpsideDown ? -0.5 : 0.5);
	}
	
	States
	{
		Spawn:
			MVP1 A -1 Light ("50CalTracerLight");
			Stop;
		Death: //Bullet hit level geometry.
			TNT1 A 1
			{
				//The floor was hit.
				If (Pos.Z <= FloorZ)
				{
					Switch (CheckLiquid (FloorPic))
					{
						Case LIQUID_WATER:
							Do50BMGSplash (LIQUID_WATER);
							Break;
						Case LIQUID_NUKAGE:
							Do50BMGSplash (LIQUID_NUKAGE);
							Break;
						Case LIQUID_BLOOD:
							Do50BMGSplash (LIQUID_BLOOD);
							Break;
						Case LIQUID_LAVA:
							Do50BMGSplash (LIQUID_LAVA);
							Break;
						Case LIQUID_SLIME:
							Do50BMGSplash (LIQUID_SLIME);
							Break;
						Default:
							Break;
					}
				}
				//The ceiling was hit.
				Else If (Pos.Z + Height >= CeilingZ)
				{
					Switch (CheckLiquid (CeilingPic))
					{
						Case LIQUID_WATER:
							Do50BMGSplash (LIQUID_WATER,True);
							Break;
						Case LIQUID_NUKAGE:
							Do50BMGSplash (LIQUID_NUKAGE,True);
							Break;
						Case LIQUID_BLOOD:
							Do50BMGSplash (LIQUID_BLOOD,True);
							Break;
						Case LIQUID_LAVA:
							Do50BMGSplash (LIQUID_LAVA,True);
							Break;
						Case LIQUID_SLIME:
							Do50BMGSplash (LIQUID_SLIME,True);
							Break;
						Default:
							Break;
					}
				}
				
				If (HitLiquid) Destroy(); //Splash was spawned, so just delete yourself.
				Else A_StartSound ("Vehicle/APC/MGImpact");
			}
			Stop;
		XDeath: //Bullet hit a bleeding actor.
			TNT1 A 1; //Don't play the impact sound on bleeding actors.
			Stop;
		Crash: //Bullet hit a non bleeding actor.
			TNT1 A 1 A_StartSound ("Vehicle/APC/MGImpact");
			Stop;
	}
}

//A smaller bullet projectile sort of based on the 7.62mm NATO round.
Class MVP_RifleBullet : MVP_BaseProjectile
{
	Default
	{
		Radius 4;
		Height 6;
		Speed 200; //IRL, the M240 has a slightly slower muzzle velocity than the M2 Browning, so the rifle round is slightlky slower too, but not as slow as the autocannon.
		//I mean, autocannon shells are also fired at a higher muzzle velocity than bullets, yet the autocannon projectile is SLOWER instead of faster, but whatever.
		FastSpeed 220;
		DamageFunction (Random (10,16)); //The marines' rifles do 6 to 10 damage, so this is about double that. Why ? I don't fucking know, let's just say the marines use 5.62mm rounds or something.
		XScale 0.1;
		YScale 0.083333; //0.1 / 1.2
		ProjectileKickback 16;
		Decal "BulletChip";
		DamageType "RifleBullet";
		Obituary "%o found out that rifle rounds are more dangerous than in the movies";
		BounceSound "SFX/BulletRicochet";
		MVP_BaseProjectile.TrailActor "MVP_Tracer";
		MVP_BaseProjectile.InitialTrailsSkipped 3;
		MVP_BaseProjectile.MaxTrails 64;
		MVP_BaseProjectile.TrailLineSpacing 1;
	}
	
	Bool HitLiquid;
	
	Void DoRifleSplash (Int LiquidType, Bool UpsideDown = False)
	{
		HitLiquid = True;
		Actor Splash = SpawnLiquidSplash (LiquidType,"SFX/WaterHit",1.0,1.0);
		
		Splash.Scale.X = Splash.Scale.Y = (UpsideDown ? -0.25 : 0.25);
	}
	
	Override Void TrailSpawn (Actor Trail)
	{
		Super.TrailSpawn (Trail);
		
		Trail.Scale.X = Trail.Scale.Y = 0.25;
	}
	
	Override Int SpecialMissileHit (Actor Victim)
	{
		If (!Victim || Victim && Victim == DontCollide) Return 1;
		If (Target && (Victim.bIsMonster || Victim.Player) && !(Target.IsHostile (Victim))) Return 1; //Don't harm the shooter or allies.
		
		//Always bounce off players with god mode. Or player pawns and monsters with Invulnerable or NoDamage.
		If ((HasGodMode(Victim) || IsIndestructible(Victim) && (Victim.Player || Victim.bIsMonster)) && DeflectProjectile (Victim,0,FRandom(0.4,0.6),FRandom (1.,0.7),(10,-10),(10,-10))) Return 1;
		//Be at least somewhat likely to bounce off if hitting an APC.
		Else If ((Victim Is "MVP_APC" || Victim Is "MVP_APCProp") && DeflectProjectile (Victim,128,FRandom(0.4,0.6),FRandom (1.,0.7),(10,-10),(10,-10))) Return 1;
		//Be very likely to bounce off a non-bleeding actor with a lot of health.
		Else If (Victim.bNoBlood && !IsIndestructible (Victim) && Victim.SpawnHealth() >= 2000 && DeflectProjectile (Victim,64,FRandom(0.5,0.7),FRandom (1.,0.8),(6,-6),(6,-6),attenuation:0.75)) {bRipper = False; Return 1;}
		
		
		Return -1;
	}
	
	//Mom, can we have MVP_50CalBullet ?
	//No, we have MVP_50CalBullet at home.
	//The MVP_50CalBullet at home:
	States
	{
		Spawn:
			MVP1 A -1 Light ("RifleTracerLight");
			Stop;
		Death: //Bullet hit level geometry.
			TNT1 A 1
			{
				//The floor was hit.
				If (Pos.Z <= FloorZ)
				{
					Switch (CheckLiquid (FloorPic))
					{
						Case LIQUID_WATER:
							DoRifleSplash (LIQUID_WATER);
							Break;
						Case LIQUID_NUKAGE:
							DoRifleSplash (LIQUID_NUKAGE);
							Break;
						Case LIQUID_BLOOD:
							DoRifleSplash (LIQUID_BLOOD);
							Break;
						Case LIQUID_LAVA:
							DoRifleSplash (LIQUID_LAVA);
							Break;
						Case LIQUID_SLIME:
							DoRifleSplash (LIQUID_SLIME);
							Break;
						Default:
							Break;
					}
				}
				//The ceiling was hit.
				Else If (Pos.Z + Height >= CeilingZ)
				{
					Switch (CheckLiquid (CeilingPic))
					{
						Case LIQUID_WATER:
							DoRifleSplash (LIQUID_WATER,True);
							Break;
						Case LIQUID_NUKAGE:
							DoRifleSplash (LIQUID_NUKAGE,True);
							Break;
						Case LIQUID_BLOOD:
							DoRifleSplash (LIQUID_BLOOD,True);
							Break;
						Case LIQUID_LAVA:
							DoRifleSplash (LIQUID_LAVA,True);
							Break;
						Case LIQUID_SLIME:
							DoRifleSplash (LIQUID_SLIME,True);
							Break;
						Default:
							Break;
					}
				}
				
				If (HitLiquid) Destroy(); //Splash was spawned, so just delete yourself.
				Else A_StartSound ("Vehicle/APC/SmallMGImpact");
			}
			Stop;
		XDeath: //Bullet hit a bleeding actor.
			TNT1 A 1; //Don't play the impact sound on bleeding actors.
			Stop;
		Crash: //Bullet hit a non bleeding actor.
			TNT1 A 1 A_StartSound ("Vehicle/APC/SmallMGImpact");
			Stop;
	}
}

Class MVP_AutocannonShell : MVP_BaseProjectile
{
	Default
	{
		Radius 12;
		Height 16;
		Speed 180;
		FastSpeed 200;
		XScale 0.5;
		YScale 0.416666; //0.5 / 1.2
		DamageFunction (Random (60,80));
		ProjectileKickback 128;
		Obituary "%o took a 30mm round to the face";
		Decal "30mmScorch";
		DamageType "Autocannon";
		MVP_BaseProjectile.TrailActor "MVP_Tracer";
		MVP_BaseProjectile.TrailDelay 1;
		MVP_BaseProjectile.InitialTrailsSkipped 16;
		MVP_BaseProjectile.TrailFadeAndScaleStart 64;
		MVP_BaseProjectile.TrailLineSpacing 4;
		MVP_BaseProjectile.MaxTrails 128;
		MVP_BaseProjectile.ScaleAmountX -0.05;
		MVP_BaseProjectile.ScaleAmountY -0.05;
		MVP_BaseProjectile.FadeAmount -0.05;
	}
	
	Override Void TrailSpawn (Actor Trail)
	{
		Super.TrailSpawn (Trail);
		
		Trail.Scale.X = Trail.Scale.Y = 1.1;
	}
	
	Override Int SpecialMissileHit (Actor Victim)
	{
		If (!Victim) Return 1;
		If (Target && (Victim.bIsMonster || Victim.Player) && !(Target.IsHostile (Victim))) Return 1; //Don't harm the shooter or allies.
		
		If (Victim.SpawnHealth() <= 400) bExtremeDeath = True; //Always gib weak enough enemies.
		Else bExtremeDeath = False;
		
		Return -1;
	}
	
	Void DoAutocannonSplash (Int LiquidType, Bool UpsideDown = False)
	{
		HitLiquid = True;
		Actor Splash = SpawnLiquidSplash (LiquidType,explosiondamage:96,48,0,'Explosive', True);
		
		Splash.Scale.X = Splash.Scale.Y = (UpsideDown ? -2.0 : 2.0);
	}
	
	Bool HitLiquid;
	
	States
	{
		Spawn:
			MVP1 C 0;
			MVP1 # 4 Light ("AutocannonTracer")
			{
				A_FaceMovementDirection ();
				Frame = UpdateProjectileElevation (5,6,2,3,4); //F, G, C, D, E
			}
			Wait;
		Death: //Shell hit level geometry.
			TNT1 A 0
			{
				//The floor was hit.
				If (Pos.Z <= FloorZ)
				{
					Switch (CheckLiquid (FloorPic))
					{
						Case LIQUID_WATER:
							DoAutocannonSplash (LIQUID_WATER);
							Break;
						Case LIQUID_NUKAGE:
							DoAutocannonSplash (LIQUID_NUKAGE);
							Break;
						Case LIQUID_BLOOD:
							DoAutocannonSplash (LIQUID_BLOOD);
							Break;
						Case LIQUID_LAVA:
							DoAutocannonSplash (LIQUID_LAVA);
							Break;
						Case LIQUID_SLIME:
							DoAutocannonSplash (LIQUID_SLIME);
							Break;
						Default:
							Break;
					}
				}
				//The ceiling was hit.
				Else If (Pos.Z + Height >= CeilingZ)
				{
					Switch (CheckLiquid (CeilingPic))
					{
						Case LIQUID_WATER:
							DoAutocannonSplash (LIQUID_WATER,True);
							Break;
						Case LIQUID_NUKAGE:
							DoAutocannonSplash (LIQUID_NUKAGE,True);
							Break;
						Case LIQUID_BLOOD:
							DoAutocannonSplash (LIQUID_BLOOD,True);
							Break;
						Case LIQUID_LAVA:
							DoAutocannonSplash (LIQUID_LAVA,True);
							Break;
						Case LIQUID_SLIME:
							DoAutocannonSplash (LIQUID_SLIME,True);
							Break;
						Default:
							Break;
					}
				}
				
				If (HitLiquid) Destroy(); //Splash was spawned, so just delete yourself.
			}
			TNT1 A 0
			{
				A_SetScale (1.5,1.5);
				A_SetRenderStyle (1.0,Style_Add);
				If (Pos.Z + Height >= CeilingZ) Scale.Y = -1.5; //If a ceiling was hit, invert the explosion.
				Else If (Pos.Z >= FloorZ+24) Return ResolveState ("AirBlast"); //If the explosion happened above 16 map units from the ground, trigger the air explosion.
				//SetOrigin ((Pos.X,Pos.Z,FloorZ),False); //Otherwise stick to the ground to explode if you didn't hit level geometry too far above the ground.
				Return State (Null);
			}
			GXPL A 2 Bright;
			GXPL B 4 Bright
			{
				A_StartSound ("Vehicle/APC/AutocannonHit",attenuation:0.3);
				MakeAlliesImmuneToExplosion (128);
				A_Explode (96,128,XF_EXPLICITDAMAGETYPE,True,32,damagetype:'Explosive');
				RemoveAllyExplosionImmunity();
			}
			GXPL CDEFGHIJKLMNO 4 Bright;
			Stop;
		XDeath: //Shell hit a bleeding actor.
			Goto AirBlast;
		Crash: //Shell hit a non bleeding actor.
			Goto AirBlast;
		AirBlast:
			TNT1 A 0 //Stop moving in the air, and add some random rotation for variety.
			{
				A_StartSound ("Vehicle/APC/AutocannonHit",attenuation:0.3);
				A_Stop();
				A_SetRenderStyle (1.0,Style_Add);
				A_SetScale (1.5,1.5);
				Gravity = 0;
				bRollSprite = True;
				Roll = FRandom (0,180);
			}
			GAXP A 4 Bright;
			GAXP B 4 Bright
			{
				MakeAlliesImmuneToExplosion (128);
				A_Explode (96,128,XF_EXPLICITDAMAGETYPE,True,32,damagetype:'Explosive');
				RemoveAllyExplosionImmunity();
			}
			GAXP CDEFGHIJ 4 Bright;
			Stop;
	}
}

Class MVP_DestroyedMGTurretGun : Actor
{
	Default
	{
		Radius 16;
		Height 32;
		XScale 0.65;
		YScale 0.541666;
		Speed 48;
		Mass 200;
		Gravity 1;
		ProjectileKickback 48;
		DamageFunction (Random (64,96));
		DamageType "Melee";
		Obituary "%o was hit by a destroyed machine gun";
		Projectile;
		-NoBlockmap;
		-NoGravity;
		+BloodlessImpact;
	}
	
	States
	{
		Spawn:
			TNT1 A 0;
			APC3 O 1
			{
				//The turret leans upwards over time as it falls until impact.
				Switch (GetAge())
				{
					Case 35/2:
						Frame = 15;
						Break;
					Case 35:
						Frame = 16;
						Break;
					Case 50:
						Frame = 17;
						Break;
					Default:
						Break;
				}
			}
			Loop;
		Death:
			APC3 O 0 A_StartSound ("SFX/MetalHit",volume:0.75,0.5);
			APC3 O -1;
			Stop;
	}
}

Class MVP_DestroyedAPCFrontDoor : MVP_DestroyedMGTurretGun
{
	Default
	{
		Radius 12;
		Height 48;
		Speed 48;
		Mass 100;
		DamageFunction (Random (48,64));
		ProjectileKickback 16;
		Obituary "%o was hit by an APC's door";
	}
	
	Override Void Tick()
	{
		Super.Tick();
		
		If (IsFrozen ()) Return;
		//Slow down the spinning over time.
		If (ReactionTime <= 24 && GetAge() % 8 == 0)
			ReactionTime++;
	}
	
	States
	{
		Spawn:
			APC5 ABCD 2 A_SetTics (ReactionTime);
			Loop;
		Death:
			APC5 E -1 A_StartSound ("SFX/MetalHit",volume:0.5);
			Stop;
	}
}

Class MVP_APCHatch : MVP_DestroyedAPCFrontDoor
{
	Default
	{
		Radius 12;
		Height 8;
		ProjectileKickBack 8;
		DamageFunction (Random (12,24));
		Obituary "%o had an APC hatch fall from the sky";
	}
	
	States
	{
		Spawn:
			APC2 YZ[] 2 A_SetTics (ReactionTime);
			Loop;
		Death:
			APC2 Y -1 A_StartSound ("SFX/MetalHit",volume:0.5);
			Stop;
	}
}
#Include "Sounds/Vehicles/Army Car/Pain Ray/e"
Class MVP_APCWheel : MVP_DestroyedMGTurretGun
{
	Default
	{
		Radius 24;
		Height 32;
		Speed 48;
		Mass 100;
		ProjectileKickback 24;
		DamageFunction (Random (16,32));
		BounceType "Hexen";
		BounceFactor 0.75;
		BounceSound "SFX/WheelHit";
		Obituary "%o was smacked by an APC wheel";
		+CanBounceWater;
		+DontBounceOnSky;
		+BounceOnActors;
	}
	
	Override Void Tick()
	{
		Actor.Tick();
		
		If (IsFrozen() || InStateSequence(CurState,ResolveState ("Death"))) Return;
		
		//Slow down the spinning over time.
		If (ReactionTime <= 24 && GetAge() % 8 == 0)
			ReactionTime++;
		
		//Stop moving if too slow.
		If (Vel.Length() >= 2)
			BounceSound = Default.BounceSound;
		Else
		{
			BounceSound = "";
			ExplodeMissile(); //Hit the ground.
		}
	}
	States
	{
		Spawn:
			APC5 FGHI 2 A_SetTics (ReactionTime);
			Loop;
		Death:
			APC5 J -1;
			Stop;
	}
}

//Grotesque Zandronum-era hack.
Class MVP_DesolidifyToken : Inventory
{
	Default
	{
		ReactionTime 8; //Tic timer before the item leaves.
		+Inventory.Untossable;
	}
	
	Override Void AttachToOwner(Actor Other)
	{
		Super.AttachToOwner(Other);
		
		If (Other)
			Other.bSolid = False;
	}
	
	Override Void DoEffect()
	{
		If (!ReactionTime)
			GoAwayAndDie();
		If (ReactionTime)
			ReactionTime--;
	}
	
	Override Void DetachFromOwner()
	{
		Super.DetachFromOwner();
		If (Owner)
			Owner.bSolid = True;
	}
}

//An alias for the APC, which can be used to spawn the APC through the console with the machine gun included.
Class MVP_APC_MG : MVP_APC
{
	//Spawn only the MG.
	Override Void SpawnVehicleTurret ()
	{
		Turret = MVP_BaseTurret(Spawn ("MVP_APCMachineGun",Pos+TurretOffsets));
		If (Turret)
		{
			Turret.CopyFriendliness (Self,False);
			Turret.Angle = Angle;
			Turret.Pitch = Pitch;
			Turret.Master = Self;
			Turret.TurretOffsets = TurretOffsets;
			User_Turret = "Machine Gun";
			SetTag ("Armored Personnel Carrier (Machine Gun)");
		}
	}
}

Class MVP_APC_Autocannon : MVP_APC
{
	//Spawn only the autocannon.
	Override Void SpawnVehicleTurret ()
	{
		Turret = MVP_BaseTurret(Spawn ("MVP_APCAutocannon",Pos+TurretOffsets));
		If (Turret)
		{
			Turret.CopyFriendliness (Self,False);
			Turret.Angle = Angle;
			Turret.Pitch = Pitch;
			Turret.Master = Self;
			Turret.TurretOffsets = TurretOffsets;
			User_Turret = "Autocannon";
			SetTag ("Armored Personnel Carrier (Autocannon)");
		}
	}
}