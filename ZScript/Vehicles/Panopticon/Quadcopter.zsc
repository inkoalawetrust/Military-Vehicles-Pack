Class MVP_Quadcopter : MVP_BaseFlyer
{
	Default
	{
		//$Title Quadcopter
		//$Category Vehicles/Panopticon
		//$Sprite PND2B1
		
		Tag "Akcipitro 2A"; //A = Armed/Gunner
		KAI_Actor.ThreatLevelThreshold THREAT_DANGEROUS;
		KAI_Actor.ThreatLevel THREAT_NORMAL;
		KAI_Actor.FlightAcceleration 0.25;
		KAI_Actor.FlightDeceleration 0.25;
		KAI_Actor.FlightWrongDirFactor 2.5;
		MVP_BaseVehicle.SmokeDelay 8;
		KAI_BaseVehicle.HeadlightLevel 88;
		KAI_Actor.Inaccuracy (32,32,32);
		KAI_Actor.InaccuracyFrequency 0.6;
		KAI_Actor.ActorFollowDist 192;
		
		+WindThrust;
		//+Pushable;
		//+SlidesOnWalls;
		+FloatBob; //Scales with speed
		+LookAllAround;
		+AvoidMelee;
		+MVP_BaseVehicle.Drone;
		
		Health 40;
		WoundHealth 20;
		GibHealth 30;
		XScale 0.3;
		YScale 0.25; //0.3/1.2
		FloatSpeed 1;
		FloatBobStrength 0.5;
		Mass 35;
		Height 14;
		DeathHeight 8;
		Radius 16;
		CameraHeight 5;
		MaxStepHeight 10;
		MeleeRange 64;
		Obituary "%o died from a drone strike";
		ActiveSound "Vehicle/Drone/Fly";
		AttackSound "Vehicle/APC/SmallMGFire";
		Speed 10;
		FastSpeed 16;
		//Friction 0.5;
		RipLevelMin 1;
	}
	
	Override Void PostBeginPlay()
	{
		Super.PostBeginPlay();
		
		If (KAIHandler) Yapper = KAIHandler.GetNPCVoice ("Panopticon");
		If (!User_NoWalking)
		{
			If (!TestMobjZ(True) || GetZAt ()+1 >= Pos.Z)
				MVP_QuadcopterLand();
			Else
				MVP_QuadcopterTakeOff();
		}
		
		If (Self Is "MVP_GyrojetQuadcopter")
		{
			ProjSpeed = 50;
			ProjFastSpeed = 70;
			ProjTurn = 10;
		}
		Else
		{
			ProjSpeed = 170;
			ProjFastSpeed = 200;
			ProjTurn = 5;
		}
		
		LandParams.AngleLimit = TurnHorz;
		LandParams.DetourFactor = DetourFactor;
		AttackStates.Push (FindState("Missile.Ground",True));
	}
	
	Override Void UserVariableDefaults()
	{
		If (User_ChaseTime == 0) User_ChaseTime = 35*30;
		If (User_RetreatDistance == 0) User_RetreatDistance = 384;
		If (User_DodgeRange == 0) User_DodgeRange = 384;
	}
	
	Override Void SpawnHeadlights()
	{
		Headlights.Push (SpawnVehicleHeadlight (Self,(16,0,0),VHSF_SPRITEROT,(255,0,0),128,(15,80)));
	}
	
	Override Bool CanAttack()
	{
		If (CurSector.Flags & Sector.SECF_NOATTACK) Return False;
		Return True;
	}
	
	Override Bool SpawnDamageSmoke()
	{
		If (!Super.SpawnDamageSmoke()) Return False;
		Vector3 SpawnPos = Level.Vec3Offset (Pos,(FRandom(-8,8),FRandom(-8,8),FRandom(2,8)));
		Actor Smoke = Spawn ("MVP_DamageSmoke",SpawnPos);
		Smoke.Roll = FRandom(-180,180);
		Smoke.A_SetScale (FRandom(0.1,0.5));
		Smoke.Vel.Z = FRandom (1,2);
		Return True;
	}
	
	Override Void Tick()
	{
		Super.Tick();
		If (IsFrozen())
			Return;
		
		If (!Target && IdleWalkTimer > 0)
			IdleWalkTimer--;
		
		Bool Attacking = IsInAttackState();
		
		If (FireDelay && !Attacking) FireDelay--;
		
		If (Landed) //When you walking.
		{
			If (IsInState(Self,"Missile.Ground")) AimAtTarget (ProjTurn,ProjTurn,flags:FAF_MIDDLE,attackspeed:ProjSpeed,ProjFastSpeed);
			Return;
		}
		
		Bool Ded = IsDead(Self);
		//Scale visual bobbing with speed.
		If (!Ded)
		{
			FloatBobStrength = KAI_Math.LinearMap (Vel.Length(),0,6,Default.FloatBobStrength,0.05);
			FloatBobStrength = Clamp (FloatBobStrength,0,Default.FloatBobStrength);
		}
		//Console.Printf ("Bob strength %.2f",FloatBobStrength);
		
		If (!Attacking)
		{
			If (!Ded && Target && Target != Goal) //Adjust imaginary turret, as long as the target isn't actually a goal point. So that bChaseGoal works.
			{
				Double RealAngle = Angle; Double RealPitch = Pitch;
			
				Angle = FireAngle; Pitch = FirePitch;
				FireAngle = GetAngleToPos (Target.Pos,ProjTurn);
				FirePitch = GetPitchToPos (Target.Pos,ProjTurn);
				Angle = RealAngle; Pitch = RealPitch;
				//FirePitch = PitchTo (Target);//-GetPitchToPos (Target.Pos,5); //HACK: Pitches are fucking haunted in GZDoom, so we can't increment THAT one over time!
				//FirePitch = Clamp (FirePitch,MaxGunPitch,90);
				//kai_lofraycast.visualizetracepath (Pos,level.vec3offset(pos,kai_math.angletovector3d(fireangle,-firepitch,256)),256);
			}
			//Replace sprites to make it visually lean based on how high its' velocity is and where it's heading relative to its' current angle.
			If (!Ded)
			{
				//HACK: Frames are offset by -1 to stop the states from flickering to the default state sprite for one tick on every state change.
				//KLUDGE: Oh also do NOT shift them by +1 instead, as that makes them basically switch every tick regardless of the state machine!
				Switch (Frame)
				{
					Case 2: //A
						Frame = LeanFlyerAround (1,4,0,4,offset:SpriteRotation); // B, E, A
						Break;
					Case 0: //B
						Frame = LeanFlyerAround (2,5,0,4,offset:SpriteRotation); // C, F, A
						Break;
					Case 1: //C
						Frame = LeanFlyerAround (3,6,0,4,offset:SpriteRotation); // D, G, A
						Break;
					Default:
						Break;
				}
			}
			Else If (Frame == 24) //We only care about frame Y.
				Frame = LeanFlyerAround (22,23,24); //W, X, Y
		}
		Else
		{
			//We aim ahead using a separate set of pitches and angles, allowing the drone to not have to actually change angle and pitch mid move.
			Double RealAngle = Angle; Double RealPitch = Pitch;
			
			Angle = FireAngle; Pitch = FirePitch;
			AimAtTarget (ProjTurn,ProjTurn,flags:FAF_MIDDLE,attackspeed:ProjSpeed,ProjFastSpeed);
			FireAngle = Angle; FirePitch = Pitch;
			
			Angle = RealAngle; Pitch = RealPitch;
			console.printf ("attack angle %d, real angle %d, attack pitch %d, real pitch %d",fireangle,angle,firepitch,pitch);
			MVP_QuadcopterChase(); //Oh yes, we can run n' gun quite fine.
			
			If (FirePitch >= 15)
			{
				If (Frame == 0) //A
					Frame = 8; //I
				Else If (Frame == 7) //H
					Frame = 9; //J
			}
		}
	}
	
	Override Vector3 GetAttackLocation() //Wherever we're aiming.
	{
		LineTrace (FireAngle,MaxTargetRange,FirePitch,TRF_THRUHITSCAN|TRF_SOLIDACTORS,data:Ground);
		Return (Ground.HitLocation);
	}
	
	Override String GetObituary (Actor Victim, Actor Inflictor, Name MOD, Bool PlayerAttack)
	{
		If (Landed)
			Return "%o got %p knees popped by a drone";
		Return Super.GetObituary (Victim, Inflictor, MOD, PlayerAttack);
	}
	
	Override Bool ShouldAttack (Bool NoStateJump)
	{
		If (!CanAttack() || IsInAttackState() || Target == Goal || FireDelay > 0) //Don't run from within attack states to allow the drone to run n' gun by calling its' chase function.
			Return False;
		
		If (MVP_QuadcopterLOF() && KAI_CheckMissileRange (Target,CMR_3DDISTANCE|CMR_NOHITFRIEND))
		{
			If (!NoStateJump)
			{
				If (!Landed)
					SetStateLabel ("Missile");
				Else
				{
					If (!(Self Is "MVP_GyrojetQuadcopter")) LandBullets = Random (8,16);
					Else LandBullets = Random (6,12); //Don't fire as many gyrojets.
					SetStateLabel ("Missile.Ground");
				}
			}
			
			Return True;
		}
		
		Return False;
	}
	
	Void MVP_QuadcopterChase ()
	{
		KAI_Chase_PreChecks();
		If (IsDead (Target)) Target = Null;
		KAI_Chase_Retarget(0,True);
		
		Bool MovedAlready; Bool B;
		Int KMTFlags = !Landed ? KMT_3D : 0;
		
		//DETERMINE IF WE SHOULD BE MOVING AWAY FROM THE TARGET.
		Bool RunAway = (!bNoFear && (IsFrightening (Target) || bFrightened)); //Run away from spooky actors, or maybe we're just a little bitch.
		//If we're set to run away from targets over our threat threshold that get too close, do so.
		Double RunRange = User_RetreatDistance;
		Int TargetLevel;
		If (Target)
		{
			RunRange += Target.Radius;
			TargetLevel = AssessThreatLevel(Target, True);
		}
		
		Bool TargetInSight = CheckSight (Target,SF_IGNOREWATERBOUNDARY);
		Bool LockedIn = bChaseGoal && Goal; //Don't run away if you have a target but are also patrolling in bChaseGoal mode. Since you do that by just flying around.
		Bool BigThreat = (ThreatLevelThreshold != THREAT_ASSESS && TargetLevel >= ThreatLevelThreshold); //Target's threat level is over the threshold.
		If (RunRange != -1 && !bNoFear && Target && Target != Goal && !LockedIn && !RunAway && BigThreat && RunRange > 0 && Distance3DSquared(Target) <= RunRange*RunRange && TargetInSight)
			RunAway = True;
		
		//Avoid melee attackers.
		If (!LockedIn && !RunAway && Target && (bAvoidMelee || Level.Info.Flags3 & LEVEL3_AVOIDMELEE))
		{
			Bool IsMeleeAttacker;
			If (!Target.Player && Target.MeleeState) //Melee NPC
			{
				Double Dist = (Target.MeleeRange + Target.Radius)*2;
				IsMeleeAttacker = Distance3DSquared (Target) < Dist*Dist;
			}
			Else If (Target.Player && Target.Player.ReadyWeapon && Target.Player.ReadyWeapon.bMeleeWeapon) //Melee player
				IsMeleeAttacker = Distance3DSquared(Target) <= 192*192;
			
			If (Landed && IsMeleeAttacker) //Fly out of melee.
			{
				SetStateLabel ("TakeOff");
				Return;
			}
			
			RunAway = IsMeleeAttacker;
		}
		
		If (Landed && (TargetLevel > ThreatLevelThreshold || Health <= WoundHealth)) //Fly away from very dangerous target or when too injured.
		{
			SetStateLabel ("TakeOff");
			Return;
		}
		
		If (Health > WoundHealth) //Ignore weak hazards if not weak yourself.
		{
			For (Int I = Hazards.Size()-1; I >= 0; I--)
			{
				If (Hazards[I] && Hazards[I].Level <= HazardInfo.HAZARD_LOW)
					Hazards.Delete(I);
			}
		}
		HazardInfo Haz = GetNearestHazard(Hazards);
		Double HazardDist = GetHazardDistance (Haz); //Get distance to nearest hazard that you're in range of.
		
		//Keep different distances for targets under the threshold.
		If (!LockedIn && !RunAway && Target && (TargetLevel < ThreatLevelThreshold || RunRange != -1))
		{
			Double Dist = 0;
			Switch (TargetLevel)
			{
				//Case THREAT_UNSTOPPABLE:
				//	Dist = 8192;
				//	Break;
				Case THREAT_SUPERDANGER:
					Dist = 768;
					Break;
				Case THREAT_VERYDANGEROUS:
					Dist = 512;
					Break;
				Case THREAT_DANGEROUS:
					Dist = 384;
					Break;
				//Just in case the threshold is changed ^^^^^
				Case THREAT_ABOVENORMAL:
					Dist = 192;
					Break;
				Case THREAT_NORMAL:
					Dist = 128;
					Break;
				Case THREAT_MILD:
				Case THREAT_LOW:
					Dist = 32;
					Break;
				Case THREAT_VERYLOW:
				Default:
					Break;
			}
			If (Dist > 0) Dist += Target.Radius;
			If (Health <= WoundHealth) //Keep some extra distance when hurt.
				Dist += 32;
			
			Double TargDist = Distance3D (Target);
			//Run from target if hazard isn't closer.
			//BUG: Yeah so this just doesn't run if we have a hazard in range I guess? Isn't that just splendid?
			If (Dist && TargDist <= Dist && !(Haz && TargDist >= HazardDist))
			{
				RunAway = True;
				Haz = Null; //Signify that we shouldn't run from a hazard later in the function.
			}
		}
		//Also check for scary hazards.
		If (Haz)
			RunAway = True;
		
		Actor Proj;
		If ((Proj = MVP_DroneDodge(!Landed ? User_DodgeRange : User_DodgeRange*3,LastProjectile)))
		{
			If (!Landed)
			{
				Double Delta = DeltaAngle (Angle,AngleTo(Proj));
				If (Delta >= -2)
					KMT_3D_SpeedLimit(15,(AngleToVector (Angle-90,15),0));
				Else
					KMT_3D_SpeedLimit(15,(AngleToVector (Angle+90,15),0));
				
				LastProjectile = Proj; //So we don't constantly dodge the same thing.
			}
			Else
			{
				SetStateLabel ("TakeOff");
				Return;
			}
		}
		
		//Decide to land sometimes when attacking close to the ground. Especially if crammed.
		Double SectorHeight = (CeilingZ - FloorZ);
		If (!User_NoWalking && !Landed)
		{
			Int LandChance = (SectorHeight >= 128) ? 1536 : 512;
			If (Health > WoundHealth && !BigThreat && !LockedIn && !RunAway && (Pos.Z - FloorZ) <= 96 && Random(0,LandChance) == Random (0,LandChance))
			{
				SetStateLabel ("Land.Landing");
				Return;
			}
		}
		
		If (!LockedIn && RunAway)
		{
			If (!Landed) //Push yourself away from the enemy or hazard.
			{
				A_FaceTarget(TurnHorz,TurnVert);
				//If there's already a prior position to move to, begin moving there instead. Until within melee range of that position.
				If (!KAI_Math.IsEmptyVector3 (NextMovePos) && NewPosStepCount <= 16 && Level.Vec3Diff (Pos,NextMovePos).Length() >= MeleeRange)
				{
					KMT_3D_SpeedLimit (Speed/2,KAI_Math.AngleToVector3D(GetAngleToPos(NextMovePos,0),-GetPitchToPos(NextMovePos,0),FlightAcceleration));
					NewPosStepCount++;
					Return;
				}
				//Once the position is reached, get a new one. Do take a few extra steps away though.
				Else If (NewPosStepCount++ <= 24)
				{
					Vector3 RunPos = !Haz ? Target.Pos : GetHazardOriginPos(Haz);
					NextMovePos = FindRandomMovePos(RunPos,full3D:True);
					NewPosStepCount = 0;
				}
			}
			Else //Turn around.
			{
				Vector3 RunPos = !Haz ? Target.Pos : GetHazardOriginPos(Haz);
				KAI_MoveAway (Null,kmaflags:KMA_STRAIGHT,RunPos);
			}
			MovedAlready = True;
		}
		
		//Stay slightly off the ground when not in a tight space.
		If (!Landed && SectorHeight >= 72)
		{
			LineTrace (0,32,90,TRF_BLOCKSELF,data:Ground);
			If (Ground.Distance < 32)
				KMT_3D_SpeedLimit (Speed,(0,0,0.1));
		}
		
		//Abuse turrets by going over their pitch limits if possible.
		If (!Landed && (CeilingZ - Pos.Z+Height) >= 128 && Target && Target Is "KAI_BaseVehicle")
		{
			KAI_BaseTurret Tur = KAI_BaseTurret (Target);
			If (!Tur && KAI_BaseVehicle(Target) && KAI_BaseVehicle (Target).Turret) //Target isn't a turret. Maybe it's a vehicle WITH a turret?
				Tur = KAI_BaseVehicle (Target).Turret;
			
			//If we are targeting a turret or a turreted vehicle. Bully it.
			If (Tur && Tur.IsOverPitchLimits (Self,padding:10) == Tur.ELEVATION_INBOUNDS)
			{console.printf ("cyberbullying");
				KMT_3D_SpeedLimit (Speed,(0,0,0.75));
			}
		}
		
		//Target is too high up to fire at.
		Bool CantAttack;
		If (Target && TargetInSight && !(GetClass() == "MVP_GyrojetQuadcopter") && PitchTo (Target) <= MaxGunPitch)
		{console.printf ("drone rising up to fire");
			If (Landed)
			{
				SetStateLabel ("TakeOff");
				Return;
			}
			CantAttack = True;
			KMT_3D_SpeedLimit (Speed,(0,0,0.5));
		}
		
		If (!Landed)
		{
			Vector3 ScaleObs = ScaleInAdvance (384);
			If (!KAI_Math.IsEmptyVector3(ScaleObs))
				KMT_3D_SpeedLimit (Speed,(0,0,FloatSpeed));
		}
		Else If (Random (0,255) < 64)
			A_StartSound ("Vehicle/Drone/ClinkWalk",attenuation:ATTN_STATIC);
		
		//Move to target, and also handle patrolling but then seeing a target.
		If (Target && Target != Goal && !(IsPatrolling(Self) && bChaseGoal) && !MovedAlready)
		{
			If (Landed)  //Occasionally check if we can still reach our target while on the ground, and take off if we haven't been able to do so for too long.
			{
				If (Random(0,255) < 8)
				{
					If (!IsPosReachable (Target.Pos,Target.Radius*1.5,Radius))
						TargetLandAttempts++;
					Else
						TargetLandAttempts = 0;
					console.printf ("targetlandattempts %u",targetlandattempts);
				}
					
				If (TargetLandThreshold <= TargetLandAttempts)
				{
					SetStateLabel("TakeOff");
					Return;
				}
			}
			
			KAI_MoveTowards (Target.Pos.PlusZ(Target.Height/2),DetourFactor,TurnHorz,flags:KMTFlags,TurnVert);
			MovedAlready = True;
		}
		
		//Patrol handling.
		//BUG: ZDoom's hacky usage of ReactionTime makes the drone not wait at patrol points when it has a target.
		If (!MovedAlready && !Landed)
		{
			[B, MovedAlready] = KAI_Chase_PatrolHandling ("Spawn",DetourFactor,TurnHorz,flags:KMTFlags,TurnVert);
			If (bChaseGoal && Goal && Target != Goal) //Manually go to goal when it "don't chase target" mode.
			{
				KAI_MoveTowards (Goal.Pos,DetourFactor,TurnHorz,flags:KMTFlags,TurnVert);
				//Also handle visually offseting the drone to the imaginary turrets' position when we have a target.
				If (TargetInSight) SpriteRotation = DeltaAngle (Angle,FireAngle);
				Else SpriteRotation = 0;
			}
			Else
				SpriteRotation = 0;
			//If (b && goal) spawn ("kai_debugpoint",goal.pos);
			if (goal)console.printf ("patrolling, goal is at %d %d %d, moved already %d, reached point %d, reactiontime %d",goal.pos,movedalready,b,reactiontime);
		}
		Else If (Landed && Goal) //Follow patrol routes by air.
		{
			SetStateLabel ("TakeOff");
			Return;
		}
		
		If (!CantAttack && ShouldAttack())
			Return;
		
		HandleChaseTimer();
	}
	
	Vector3 ScaleInAdvance (Double Range, Actor Ignore = Null)
	{
		Vector3 Where = (Double.NaN, Double.NaN, Double.NaN); Bool Success;
		FLineTraceData Check;
		LineTrace (Angle,Range,-Pitch,TRF_BLOCKSELF,data:Check);
		Vector3 HitPos = Check.HitDir * (Check.Distance-(Radius*0.5)); //Can't be the exact hit location outside ScaleObstacle just returns false.
		HitPos = Level.Vec3Offset(Pos,HitPos);
		
		If (Check.HitType && !(Ignore && Check.HitActor && Ignore == Check.HitActor))
			Success = KMT_3D_ScaleObstacle(HitPos,Where.Z);
		
		If (!Success)
			Where = (Double.NaN, Double.NaN, Double.NaN);
		Else
			Where.XY = HitPos.XY;
		
		kai_lofraycast.visualizetracepath (Pos,hitpos,check.distance);
		
		Return Where;
	}
	
	//Return true if we are aiming ahead of our target and also have a valid LOF straight at them.
	Bool MVP_QuadcopterLOF()
	{
		//If (!AimingAheadOfTarget())
		If (!Target)
			Return False;
			
		Let Check = New ("KAI_ProjectileLOFCheck");
		
		Check.Shooter = Self;
		Check.Other = Target;
		//Check.SplashRadius = SplashRadius;
		Check.PropHealthThreshold = 50;
		Check.FriendlyFire = False;
		
		Check.ProjInfo = New ('LOFProjInfo'); //Assign data class.
		Check.ProjInfo.Projectile = "MVP_SmallRifleBullet"; //Pass projectile class to class.
		Check.ProjInfo.InitializeProjectileInfo(); //Let it infer the rest.
		
		Check.Trace (Pos.PlusZ(4),CurSector,KAI_Math.AngleToVector3D(AngleTo(Target),PitchTo(Target,targzofs:Target.Height/2)),Distance3D (Target)+32,0);
		
		If (Check.BadLOF)
		{
			If (Check.ProjInfo) Check.ProjInfo.Destroy();
			Check.Destroy();
			Return False;
		}
		
		If (Check.ProjInfo) Check.ProjInfo.Destroy();
		Check.Destroy();
		Return True;
	}
	
	Void MVP_QuadcopterFire ()
	{
		A_StartSound (AttackSound,CHAN_WEAPON,CHANF_OVERLAP,1.0,0.3);
		Actor Proj;
		If (!(Self Is "MVP_GyrojetQuadcopter"))
		{
			If (!Landed)
			{
				Proj = A_SpawnProjectile ("MVP_SmallRifleBullet",4,angle:FireAngle,flags:CMF_AIMDIRECTION|CMF_ABSOLUTEANGLE,FirePitch); //pew pew
				Vel += KAI_Math.AngleToVector3D (Angle,Pitch,-2.5);
			}
			Else
			{
				Proj = A_SpawnProjectile ("MVP_SmallRifleBullet",4,flags:CMF_AIMDIRECTION,Pitch); //pew pew
				A_Recoil(1);
			}
		}
		Else
		{
			If (!Landed)
			{
				Proj = A_SpawnProjectile ("MVP_Gyrojet",4,angle:FireAngle,flags:CMF_AIMDIRECTION|CMF_ABSOLUTEANGLE,FirePitch);
				Proj.Tracer = Target;
			}
			Else
			{
				Proj = A_SpawnProjectile ("MVP_Gyrojet",4,flags:CMF_AIMDIRECTION,Pitch); //pew pew
				Proj.Tracer = Target;
			}
		}
		AddProjectileSpread (Proj,(0.3,-0.3),(0.6,-0.6));
		FireDelay = 35*Random (2,4);
		If (!(Self Is "MVP_GyrojetQuadcopter"))
		{
			Bool B; Actor Casing;
			[B, Casing] = A_SpawnItemEx ("MVP_BulletCasing",0,0,0,FRandom(2,-2),FRandom(2,8),FRandom(2,6));
			Casing.Scale.X = 0.1; Casing.Scale.Y = 0.083333; 
		}
	}
	
	//Speak with other quadcopters.
	Void MVP_QuadcopterSpeak()
	{
		Array <Actor> Frens;
		ForEach (Grp : Groups)
		{
			If (Grp && Grp Is "MVP_DroneSwarm" && MVP_DroneSwarm(Grp).Quadcopters.Size())
			{
				ForEach (Fren : MVP_DroneSwarm(Grp).Quadcopters)
				{
					If (Fren != Self && Distance3DSquared (Fren) <= 512*512)
						Frens.Push (Fren);
				}
			}
		}
		
		If (Frens.Size())
		{
			Actor Fren = Frens[Random(0,Frens.Size()-1)];
			console.printf ("what is bro waffling on about");
			SayVoiceLine (VOICE_SEE,Yapper);
			If (Random (True, False) == True) //Sometimes the other drone speaks back.
				MVP_Quadcopter(Fren).SayVoiceLine (VOICE_SEE,MVP_Quadcopter(Fren).Yapper);
		}
	}
	
	Override Bool OnWander()
	{
		KAI_Look(LOF_NOJUMP,extraflags:KAIL_CHASETARGET);
		If (Target)
		{
			Speed = GetDefaultSpeed(GetClass());
			If (!Landed) SetState(SeeState);
			Else SetStateLabel ("See.Ground");
			Return False;
		}
		
		If (CurrentOrder == ORDER_STAY)
		{
			If (!Landed) Vel *= 0.9;
			Else Frame = 14; //Stay at PND2O when on the ground.
			Return False;
		}
		
		Actor Proj;
		If ((Proj = MVP_DroneDodge(!Landed ? User_DodgeRange : User_DodgeRange*3,LastProjectile)))
		{
			If (!Landed)
			{
				Double Delta = DeltaAngle (Angle,AngleTo(Proj));
				If (Delta >= -2)
					KMT_3D_SpeedLimit(15,(AngleToVector (Angle-90,15),0));
				Else
					KMT_3D_SpeedLimit(15,(AngleToVector (Angle+90,15),0));
				
				LastProjectile = Proj; //So we don't constantly dodge the same thing.
			}
			Else
			{
				Speed = GetDefaultSpeed(GetClass());
				SetStateLabel ("TakeOff");
				Return False;
			}
		}
		
		Vector3 FolPos = (Double.NaN, Double.NaN, Double.NaN); Actor Followee;
		Bool Following; //We followed something this time.
		If (CurrentOrder == ORDER_FOLLOW)
			[FolPos, Followee] = HandleFollowingActor (FolPos,True); //NOTE: We turn bDontFollowPlayers on between KAI_Wander calls so lonely drones in a swarm don't do that.
		
		//Scale obstacles in advance, unless we're following the thing we detect.
		If (!Landed)
		{
			Vector3 ScaleObs = ScaleInAdvance (384,Followee);
			If (!KAI_Math.IsEmptyVector3(ScaleObs))
				KMT_3D_SpeedLimit (Speed,(0,0,FloatSpeed));
		}
		Else If (Random (0,255) < 64)
			A_StartSound ("Vehicle/Drone/ClinkWalk",attenuation:ATTN_STATIC);
		
		If (Random(0,2048) == Random (0,2048) && Yapper)
			MVP_QuadcopterSpeak();
		
		//Follow something instead. Unless it's another drone that's too weak. That way pure flying drone swarms are independent-is.
		If (CurrentOrder == ORDER_FOLLOW)
		{
			If (!KAI_Math.IsEmptyVector3(FolPos))
			{
				Following = True;
				//Don't dogpile.
				If (Followee && Distance3DSquared(Followee) >= (FollowDist+Followee.Radius)*(FollowDist+Followee.Radius))
				{console.printf ("going to %s",followee.getclassname());
					If (Followee) FolPos.Z = Followee.Pos.Z+(Followee.Height/2);
					KAI_MoveTowards (FolPos,DetourFactor,TurnHorz,flags:!Landed ? KMT_3D : 0,TurnVert);
					Return False;
				}
				Else If (Followee)
				{
					If (!Landed)
					{
						A_Face (Followee,TurnHorz,TurnVert);
						//If there's already a prior position to move to, begin moving there instead. Until within melee range of that position.
						If (!KAI_Math.IsEmptyVector3 (NextMovePos) && NewPosStepCount <= 16 && Level.Vec3Diff (Pos,NextMovePos).Length() >= MeleeRange)
						{
							KMT_3D_SpeedLimit (Speed/2,KAI_Math.AngleToVector3D(GetAngleToPos(NextMovePos,0),-GetPitchToPos(NextMovePos,0),FlightAcceleration));
							NewPosStepCount++;
							Return False;
						}
						//Once the position is reached, get a new one. Do take a few extra steps away though.
						Else If (NewPosStepCount++ <= 24)
						{
							NextMovePos = FindRandomMovePos(Followee.Pos,full3D:!Landed);
							NewPosStepCount = 0;
						}
					}
					Else //If walking just turn around.
						KAI_MoveAway (Followee,kmaflags:KMA_STRAIGHT,extraparams:LandParams);
				}
			}
		}
		console.printf ("IdleWalkTimer %u",IdleWalkTimer);
		If (Landed && !IdleWalkTimer)
		{
			Speed = GetDefaultSpeed(GetClass());
			SetStateLabel ("TakeOff");
			Return False;
		}
		
		//Let's land and walk around for a bit.
		If (!User_NoWalking && !Landed && !Following && Random(0,2048) == Random (0,2048))
		{
			SetStateLabel ("Land.Landing");
			Speed = GetDefaultSpeed(GetClass());
			Return False;
		}
		
		Return True;
	}
	
	Void MVP_QuadcopterTakeOff()
	{
		Inaccuracy = Default.Inaccuracy;
		A_StartSound (ActiveSound,flags:CHANF_LOOPING,attenuation:1.5);
		bInFloat = bNoGravity = bFloatBob = True;
		Landed = False;
		TargetLandAttempts = 0;
	}
	
	Void MVP_QuadcopterLand()
	{
		IdleWalkTimer = GameTicRate*Random (6,30); //How much time we spend idly walking before getting bored.
		Inaccuracy *= 2;
		bInFloat = bNoGravity = bFloatBob = False; //Vehicle is still marked as a flyer, both for KAI and GZDoom (+FLOAT), it's just not doing that right now.
		A_StopSound (CHAN_BODY);
		Landed = True;
	}
	
	Const TurnHorz = 15;
	Const TurnVert = 60;
	Const DetourFactor = 1.5;
	Const MaxGunPitch = -20; //How much the imaginary gun can aim up.
	Const TargetLandThreshold = 3; //How many times we can determine the target isn't reachable on land before we take off.
	
	Actor LastProjectile;
	Double FireAngle, FirePitch; //The actual orientation used when firing. Think of it like an imaginary turret actor.
	UInt FireDelay;
	FLineTraceData Ground;
	KAI_Voice Yapper;
	Int ProjSpeed, ProjFastSpeed, ProjTurn; //This can change if it's a gyrojet drone instead.
	Mixin MVP_DroneVirtuals;
	Mixin MVP_DroneFunctions;
	
	//Ground mode
	Bool Landed;
	KAIMoveParams LandParams;
	UInt IdleWalkTimer; //How much time the drone spends idly walking.
	UInt8 LandBullets; //How many rounds the drone fires while on land before stopping.
	UInt8 TargetLandAttempts; //How many times we've checked while on land if we can reach the target.
	
	Double User_DodgeRange;
	Bool User_NoWalking;
	
	States
	{
		Spawn:
			PND2 ABC 1
			{
				KAI_Look(LOF_NOJUMP,extraflags:KAIL_CHASETARGET); //NOTE: The ABC frames are replaced dynamically.
				If (Target)
				{
					If (!Landed) SetState(SeeState);
					Else SetStateLabel ("See.Ground");
				}
				
				If (!Landed) KMT_3D_SpeedLimit (0,(0,0,0)); //Stand still.
				Else Frame = 14; //VISUALLY stand still.
			}
			Loop;
		See:
			PND2 ABC 1 MVP_QuadcopterChase();
			Loop;
		See.Ground:
			PND2 OPQR 4 MVP_QuadcopterChase();
			Loop;
		Idle:
			PND2 A 0 A_JumpIf (Landed,"Idle.Ground");
			PND2 ABC 1
			{
				Bool FuckYou = bDontFollowPlayers; bDontFollowPlayers = True;
				KAI_Wander (detourfactor:DetourFactor,anglelimit:TurnHorz,flags:KMT_3D,TurnVert);
				bDontFollowPlayers = FuckYou;
			}
			Goto Idle+1;
		Idle.Ground:
			PND2 OPQR 4
			{
				Bool FuckYou = bDontFollowPlayers; bDontFollowPlayers = True;
				If (Landed)
					Speed = 5;
				KAI_Wander (detourfactor:DetourFactor,anglelimit:TurnHorz);
				If (Landed)
					Speed = GetDefaultSpeed(GetClass());
				bDontFollowPlayers = FuckYou;
			}
			Loop;
		Land.Landing:
			PND2 ABC 1
			{
				KAI_Look(LOF_NOJUMP,extraflags:KAIL_CHASETARGET);
				NextMovePos = (Double.NaN,Double.NaN,Double.NaN);
				Double FS = FloatSpeed;
				If (Target) //Nvm we have a target to kill.
				{
					If (Health > WoundHealth && AssessThreatLevel (Target) > ThreatLevelThreshold)
						Return SeeState;
					Else //Or it's not too dangerous, so land twice as hard to deal with it by firing very fast
						FloatSpeed *= 2;
				}
				
				Vel *= 0.9;
				SetZ (Pos.Z-FloatSpeed*2);
				FloatSpeed = FS;
				If (!TestMobjZ(True) || GetZAt ()+1 >= Pos.Z)
					Return FindState ("Land",True);
				
				Return State(Null);
			}
			Loop;
		Land:
			PND2 K 1 MVP_QuadcopterLand();
			PND2 KLMN 6;
			Goto See.Ground;
		TakeOff:
			PND2 K 1 MVP_QuadcopterTakeoff();
			PND2 NMLK 6;
			Goto See;
		Missile:
			PND2 A 3;
			PND2 H 2 Light ("RifleFiringLightNoSelfLighting") MVP_QuadcopterFire();
			PND2 A 3;
			PND2 H 2 Light ("RifleFiringLightNoSelfLighting") MVP_QuadcopterFire();
			PND2 A 3;
			PND2 H 2 Light ("RifleFiringLightNoSelfLighting") MVP_QuadcopterFire();
			PND2 H 0
			{
				If (MVP_DroneSwitchTargets() && Random (0,255) < 8 && Yapper)
					SayVoiceLine (VOICE_SEE,Yapper);
			}
			Goto See;
		Missile.Ground:
			PND2 S 2 Light ("RifleFiringLightNoSelfLighting") MVP_QuadcopterFire();
			PND2 T 3;
			//PND2 T 0 MVP_QuadcopterSwitchTargets();
			PND2 T 0
			{
				If (LandBullets-- <= 0)
					Return FindState("See.Ground");
				
				//Target is dead or gone, or out of LOF, or too far, or too high up. So look for a new one, and stop if none can be found.
				If ((IsDead(Target) || !MVP_QuadcopterLOF() || Target && Distance3DSquared(Target) > MaxTargetRange*MaxTargetRange || PitchTo (Target) <= MaxGunPitch)
				&& !LookForPlayers(True))
					Return FindState("See.Ground");
					
				Return State (Null);
			}
			Loop;
		Death:
			TNT1 A 0
			{
				//A_Explode (32,72);
				Actor Boom = Spawn ("MVP_Explosion",Pos);
				Boom.Scale *= 0.75;
				Boom.SetDamage (32);
				Boom.MaxTargetRange = 72;
				Boom.MeleeRange = 0;
				Boom.bNoGravity = True;
				Boom.bGetOwner = True;
				Boom.Target = Self; //We exploded, this explosion is ours.
				bFloatBob = False;
				A_StopSound (CHAN_BODY);
				A_StopSound (CHAN_VOICE);
			}
			PND2 Y 1 //Falling loop.
			{
				If (!TestMobjZ(True) || GetZAt ()+1 >= Pos.Z)
					Return A_JumpIf (True == True, 1); //Hit the floor.
				
				Return State(Null);
			}
			Goto Death+1;
			PND2 UV 4;
			PND2 V -1;
			Stop;
		XDeath:
			TNT1 A 0
			{
				Actor Boom = Spawn ("MVP_Explosion",Pos);
				Boom.Scale *= 1.25;
				Boom.SetDamage (64);
				Boom.MaxTargetRange = 144;
				Boom.MeleeRange = Radius+24;
				Boom.bNoGravity = True;
				Boom.bGetOwner = True;
				Boom.Target = Self; //Does it even matter if we're gone? Very philosophical.
				
				SmokeCounter = Random (8,16);
				Bool B; Actor Debris;
				For (Int I; I <= SmokeCounter; I++)
				{
					[B, Debris] = A_SpawnItemEx ("MVP_DiCokkaDebris",FRandom (Radius,-Radius),FRandom(Radius,-Radius),FRandom (0,Height),FRandom (-6,6),FRandom (-6,6),FRandom (2,8));
					If (Debris)
					{
						Debris.Scale *= FRandom (0.1,0.4);
						Debris.bXFlip = Random (False,True);
					}
				}
				
				Spawn ("MVP_QuadcopterGib",Pos); //Just fall straight down lol.
				SmokeCounter = Random (1,4);
				For (Int I = 0; I < SmokeCounter; I++)
				{
					Actor Prop = Spawn ("MVP_QuadcopterGib",Pos);
					Prop.SetOrigin (Level.Vec3Offset(Pos,(Random (16,-16),Random (22,-22),0)),True); //Pick random position to put the propeller at.
					Prop.SmokeCounter = 1;
					Prop.Vel = (FRandom (8,-8),FRandom (8,-8),FRandom (4,8));
				}
				
				A_StopSound (CHAN_BODY);
			}
			Stop;
	}
}

Class MVP_GyrojetQuadcopter : MVP_Quadcopter
{
	Default
	{
		//$Title Quadcopter (Gyrojet)
		//$Category Vehicles/Panopticon
		//$Sprite PND2B1
		
		Tag "Akcipitro 2RK"; //RK = Raketa Kuglo
		Translation "Quadcopter_Orange";
		AttackSound ""; //The gyrojets have their own firing sound.
	}
}

Mixin Class MVP_DroneFunctions
{
	//When in a group or attacking a vehicle turret.
	Bool MVP_DroneSwitchTargets (KAI_Actor Whose = Null, Double Range = 1024)
	{
		RetargetVehicleHull (); //Target the hull of a vehicle if it makes sense.
		
		Array <Actor> Taken; //Other drones are already targeting these.
		Array <Actor> Others; //Other potential targets.
		
		If (!Whose)
			Whose = KAI_Actor(Self);
		
		ForEach (Grp : Whose.Groups)
		{
			If (Grp && Grp Is "MVP_DroneSwarm")
			{
				ForEach (Mem : Grp.Members)
				{
					If (Mem == Self)
						Continue;
					//Group member that can attack (Presumably another drone).
					If (Mem.Target && (Mem.MissileState || Mem.MeleeState) && !IsDead(Mem) && Mem.Target != Goal)
					{
						console.printf ("%p is already targeting %p",mem,mem.target);
						Taken.Push (Mem.Target);
					}
					//Turret of turreted vehicle shares target. Especially for drone *hulls* that don't attack themselves.
					Else If (KAI_BaseVehicle(Mem))
					{
						Let Tur = KAI_BaseVehicle(Mem).Turret;
						If (Tur && Tur.Master != Self && Tur.Master != Whose && Tur.Target && !IsDead(Actor(Tur)) && Mem.Target != Goal)
						{
							console.printf ("%p (%s) is already targeting %p",Tur,tur.getclassname(),tur.target);
							Taken.Push (Tur.Target);
						}
					}
				}
			}
		}
		If (!Taken.Size())
			Return False;
		
		LookForEnemiesEx (Others, Range, False, True);
		
		ForEach (Targ : Others)
		{
			If (IsDead (Targ))
				Continue;
			
			If (Taken.Find (Targ) == Taken.Size()) //This target isn't taken.
			{
				Target = Targ;
				console.printf ("found something my gang isn't targeting already");
				Return True;
			}
		}
		Return False;
	}
	
	//IDEA: Also make it handle flying away from homing projectiles maybe?
	Actor MVP_DroneDodge (Double Range = 128, Actor LastDodged = Null)
	{
		If (Range <= 0)
			Return Null;
		
		Array <Actor> Threats;
		//Actually find projectiles.
		ForEach (Proj : KAIHandler.ProjectileList)
		{
			If (!Proj || Proj == LastDodged) Continue;
			
			Bool DodgeAllyProjectiles;
			
			Bool IsHoming = Proj.bSeekerMissile; //HACK: NO stupid revenant rocket check.

			Double Dist = Distance3DSquared (Proj);
			If ((Dist > Range*Range || IsHoming && Proj.Tracer == Self && Dist > (Range*Range)*2 )) //Out of range. But if it's a seeker, check at double the range to potentially run away.
				Continue;
			
			//Dodge even friendly projectiles if they'd do too much damage, unless friendly fire is off.
			DodgeAllyProjectiles = (!(Proj Is "KAI_BaseProjectile" && KAI_BaseProjectile(Proj).bNoFriendlyFire) && Proj.GetMissileDamage (1,7) >= Health/2);
			
			If (!DodgeAllyProjectiles && !IsActorHostile (Proj.Target)) //Ignore friendly projectiles.
				Continue;
			
			//Is the projectile about to hit me ? Weird math nonsense written by RaveYard.
			double blackBox = -0.15;
			If (IsHoming && Proj.Tracer == Self) blackBox = -0.075;
			bool projectile_is_flying_towards_me = (Vec3To(proj).Unit() dot proj.vel.Unit()) < blackBox;
			double projectileSafeRadius = sqrt(2 * ((proj.radius + radius) ** 2.5));
			if(projectile_is_flying_towards_me && KAI_Math.distance3dline(pos, proj.pos, proj.pos + proj.vel) < projectileSafeRadius && CheckSight(Proj,SF_IGNOREWATERBOUNDARY))
				Threats.Push (Proj);
		}
		
		Double HighestDamage;
		Actor StrongestProj;
		For (Int I = Threats.Size()-1; I >= 0; I--)
		{
			Double CurrentDamage = Threats[I].GetMissileDamage(1,7);
			If (HighestDamage < CurrentDamage)
			{
				HighestDamage = CurrentDamage;
				StrongestProj = Threats[I];
			}
		}
		
		Return StrongestProj;
	}
}

//Drone swarm group structure. The group leader is kinda irrelevant for this group type.
//$GZDB_SKIP
Class MVP_DroneSwarm : KAI_NPCGroup
{
	Override Void PreInitialize () //No need to set group icons!
	{
		ChangeNPCGroupFlags (NPCGRP_NOICONS,0);
	}
	
	Array <Actor> Quadcopters;
	
	Override Void HandleGroupEvent (Int EventType, Actor Affected)
	{
		Super.HandleGroupEvent (EventType, Affected);
		
		//Every time the membership status of the group changes, check if there's any loitering drones in it to assign them to quadcopters randomly.
		//Also handle distinguishing said quadcopters.
		If (EventType == GRPEVNT_JOIN || EventType == GRPEVNT_LEFT || EventType == GRPEVNT_DEATH || EventType == GRPEVNT_ERASED)
		{
			If (Affected Is "MVP_Quadcopter")
			{
				If (!IsDead(Affected))
					Quadcopters.Push (Affected);
				Else //The drone is considered dead if it doesn't exist too!
					Quadcopters.Delete(Quadcopters.Find(Affected));
			}
			
			ForEach (Loit : Members)
			{
				If (Loit Is "MVP_KamikazeDrone" && Quadcopters.Size())
				{
					Loit.Master = Quadcopters[Random(0,Quadcopters.Size()-1)];
					console.printf ("randomly assigned %p to follow a quadcopter",loit);
				}
			}
		}
	}
}

Class MVP_QuadcopterGib : Actor
{
	Default
	{
		+RollSprite;
		XScale 0.5;
		YScale 0.41666; //0.5/1.2
		-Solid;
		+NoBlockmap;
	}
	
	States
	{
		Spawn:
			PND2 [ 0 NoDelay A_JumpIf (SmokeCounter == 0,"Fall"); //Gun
			PND2 ] 0 A_JumpIf (SmokeCounter == 1,"Fall"); //Rotor
		Fall:
			PND2 # 1
			{
				Roll += 20;
				If (!TestMobjZ(True) || GetZAt ()+1 >= Pos.Z)
				{
					If (SmokeCounter == 0) //Hit the floor.
					{
						Roll = 13;
						Return FindState ("Death",True);
					}
					Else
					{
						Roll = 0;
						Return FindState ("Death.Propeller",True);
					}
				}
				
				Return State(Null);
			}
			Loop;
		Death:
			PND2 [ -1;
			Stop;
		Death.Propeller:
			PND2 ^ -1;
			Stop;
	}
}

Mixin Class MVP_DroneVirtuals
{
	Override Vector3, Actor HandleFollowingActor (Vector3 BasePos, Bool IgnoreDontFollowPlayers)
	{
		Vector3 Ret; Actor Ret2;
		[Ret, Ret2] = Super.HandleFollowingActor (BasePos,IgnoreDontFollowPlayers);
		
		//Do NOT follow the player if in a swarm but this virtual returned the friendplayer. Which means the group has nobody else but you.
		If (CurrentOrder == ORDER_FOLLOW && Groups.Size() && Ret2 == Players[KAI_GetFriendPlayer()].Mo)
			Return BasePos, Null;
		
		//Do not follow other fragile drones in a swarm.
		If (MVP_BaseVehicle(Ret2) && MVP_BaseVehicle(Ret2).bDrone && AssessThreatLevel(Ret2) < THREAT_ABOVENORMAL)
			Return BasePos, Null;
		
		Return Ret, Ret2;
	}
	
	Override Void HandleNPCGroupEvent (Int EventType, Actor Affected, KAI_NPCGroup WhatGroup)
	{
		If (EventType == KAI_NPCGroup.GRPEVNT_NONE || !Affected || IsDead(Self))
			Return;
		
		If (Affected == Self)
		{
			If (EventType == KAI_NPCGroup.GRPEVNT_JOIN) //We follow when in a group.
				SetNPCOrder (ORDER_FOLLOW);
			Else If (EventType == KAI_NPCGroup.GRPEVNT_LEFT) //And go back to the defaults when not in one.
				SetNPCOrder (DefaultOrder);
		}
		
		//Swarm member died, but only speak if they are like, remotely important and also not just one of the flying bombs
		If ((EventType == KAI_NPCGroup.GRPEVNT_DEATH || EventType == KAI_NPCGroup.GRPEVNT_ERASED) && WhatGroup Is "MVP_DroneSwarm" && Random (0,255) < 8 &&
		AssessThreatLevel (Affected,True,checkdead:True) >= THREAT_MILD && !(Affected Is "MVP_KamikazeDrone"))
			SayVoiceLine (VOICE_SEE,Yapper);
	}
}